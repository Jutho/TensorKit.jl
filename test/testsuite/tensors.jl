# Tensor operations
# =================

# tensor constructions
#---------------------
@testsuite :tensors "basic properties" V -> begin
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ⊗ V3 ⊗ V4 ⊗ V5
    for T in (Int, Float32, Float64, ComplexF32, ComplexF64, BigFloat)
        t = @testinferred zeros(T, W)
        @test @testinferred(hash(t)) == hash(deepcopy(t))
        @test scalartype(t) == T
        @test norm(t) == 0
        @test codomain(t) == W
        @test space(t) == (W ← one(W))
        @test domain(t) == one(W)
        @test typeof(t) == TensorMap{T, spacetype(t), 5, 0, Vector{T}}
        # Array type input
        t = @testinferred zeros(Vector{T}, W)
        @test @testinferred(hash(t)) == hash(deepcopy(t))
        @test scalartype(t) == T
        @test norm(t) == 0
        @test codomain(t) == W
        @test space(t) == (W ← one(W))
        @test domain(t) == one(W)
        @test typeof(t) == TensorMap{T, spacetype(t), 5, 0, Vector{T}}
        # blocks
        bs = @testinferred blocks(t)
        if !isempty(blocksectors(t)) # multifusion space ending on module gives empty data
            (c, b1), state = @testinferred Nothing iterate(bs)
            @test c == first(blocksectors(W))
            next = @testinferred Nothing iterate(bs, state)
            b2 = @testinferred block(t, first(blocksectors(t)))
            @test b1 == b2
            @test eltype(bs) === Pair{typeof(c), typeof(b1)}
            @test typeof(b1) === TensorKit.blocktype(t)
            @test typeof(c) === sectortype(t)
        end
    end
end

@testsuite :tensors "dict conversion" V -> begin
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ← (V3 ⊗ V4 ⊗ V5)'
    for T in (Int, Float32, ComplexF64)
        t = @testinferred rand(T, W)
        d = convert(Dict, t)
        @test t == convert(TensorMap, d)
    end
end

@testsuite :tensors "array conversion" V -> begin
    I = sectortype(first(V))
    hasfusiontensor(I) || return nothing # trivial is also tested
    V1, V2, V3, V4, V5 = V
    W1 = V1 ← one(V1)
    W2 = one(V2) ← V2
    W3 = V1 ⊗ V2 ← one(V1)
    W4 = V1 ← V2
    W5 = one(V1) ← V1 ⊗ V2
    W6 = V1 ⊗ V2 ⊗ V3 ← V4 ⊗ V5
    for W in (W1, W2, W3, W4, W5, W6)
        for T in (Int, Float32, ComplexF64)
            if T == Int
                t = TensorMap{T}(undef, W)
                for (_, b) in blocks(t)
                    rand!(b, -20:20)
                end
            else
                t = @testinferred randn(T, W)
            end
            a = @testinferred convert(Array, t)
            b = reshape(a, dim(codomain(W)), dim(domain(W)))
            @test t ≈ @testinferred TensorMap(a, W)
            @test t ≈ @testinferred TensorMap(b, W)
            @test t === @testinferred TensorMap(t.data, W)
        end
    end
    for T in (Int, Float32, ComplexF64)
        t = randn(T, V1 ⊗ V2 ← zerospace(V1))
        a = convert(Array, t)
        @test norm(a) == 0
    end
end

@testsuite :tensors "real and imaginary parts" V -> begin
    I = sectortype(first(V))
    hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2
    for T in (Float64, ComplexF64, ComplexF32)
        t = @testinferred randn(T, W, W)

        tr = @testinferred real(t)
        @test scalartype(tr) <: Real
        @test real(convert(Array, t)) == convert(Array, tr)

        ti = @testinferred imag(t)
        @test scalartype(ti) <: Real
        @test imag(convert(Array, t)) == convert(Array, ti)

        tc = @inferred complex(t)
        @test scalartype(tc) <: Complex
        @test complex(convert(Array, t)) == convert(Array, tc)

        tc2 = @inferred complex(tr, ti)
        @test tc2 ≈ tc
    end
end

@testsuite :tensors "tensor conversion" V -> begin
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2
    t = @testinferred randn(W ← W)
    @test typeof(convert(TensorMap, t')) == typeof(t)
    tc = complex(t)
    @test convert(typeof(tc), t) == tc
    @test typeof(convert(typeof(tc), t)) == typeof(tc)
    @test typeof(convert(typeof(tc), t')) == typeof(tc)
    @test Base.promote_typeof(t, tc) == typeof(tc)
    @test Base.promote_typeof(tc, t) == typeof(tc + t)
end

# tensor contractions
#--------------------
@testsuite :tensors "full trace" V -> begin # self-consistency
    I = sectortype(first(V))
    V1, V2, V3, V4, V5 = V
    if BraidingStyle(I) isa SymmetricBraiding
        t = rand(ComplexF64, V1 ⊗ V2' ⊗ V2 ⊗ V1')
        t2 = permute(t, ((1, 2), (4, 3)))
        s = @testinferred tr(t2)
        @test conj(s) ≈ tr(t2')
        if !isdual(V1)
            t2 = twist!(t2, 1)
        end
        if isdual(V2)
            t2 = twist!(t2, 2)
        end
        ss = tr(t2)
        @tensor s2 = t[a, b, b, a]
        @tensor t3[a, b] := t[a, c, c, b]
        @tensor s3 = t3[a, a]
        @test ss ≈ s2
        @test ss ≈ s3
    end
    t = rand(ComplexF64, V1 ⊗ V2 ← V1 ⊗ V2) # avoid permutes
    ss = @testinferred tr(t)
    @test conj(ss) ≈ tr(t')
    @planar s2 = t[a b; a b]
    @planar t3[a; b] := t[a c; b c]
    @planar s3 = t3[a; a]

    @test ss ≈ s2
    @test ss ≈ s3
end

@testsuite :tensors "partial trace" V -> begin # self-consistency
    I = sectortype(first(V))
    V1, V2, V3, V4, V5 = V
    if BraidingStyle(I) isa SymmetricBraiding
        t = rand(ComplexF64, V1 ⊗ V2 ⊗ V3 ← V1 ⊗ V2 ⊗ V3)
        @tensor t2[a; b] := t[c d b; c d a]
        @tensor t4[a b; c d] := t[e d c; e b a]
        @tensor t5[a; b] := t4[a c; b c]
        @test t2 ≈ t5
    end
    t = rand(ComplexF64, V3 ⊗ V4 ⊗ V5 ← V3 ⊗ V4 ⊗ V5) # compatible with module fusion
    @planar t2[a; b] := t[c a d; c b d]
    @planar t4[a b; c d] := t[e a b; e c d]
    @planar t5[a; b] := t4[a c; b c]
    @test t2 ≈ t5
end

@testsuite :tensors "trace via conversion" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa Bosonic && hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    t = rand(ComplexF64, V1 ⊗ V2' ⊗ V3 ⊗ V2 ⊗ V1' ⊗ V3')
    @tensor t2[a, b] := t[c, d, b, d, c, a]
    @tensor t3[a, b] := convert(Array, t)[c, d, b, d, c, a]
    @test t3 ≈ convert(Array, t2)
end

#TODO: find version that works for all multifusion cases
@testsuite :tensors "trace and contraction" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa SymmetricBraiding || return nothing
    V1, V2, V3, V4, V5 = V
    t1 = rand(ComplexF64, V1 ⊗ V2 ⊗ V3)
    t2 = rand(ComplexF64, V2' ⊗ V4 ⊗ V1')
    t3 = t1 ⊗ t2
    @tensor ta[a, b] := t1[x, y, a] * t2[y, b, x]
    @tensor tb[a, b] := t3[x, y, a, y, b, x]
    @test ta ≈ tb
end

@testsuite :tensors "contraction via conversion" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa Bosonic && hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    A1 = randn(ComplexF64, V1' * V2', V3')
    A2 = randn(ComplexF64, V3 * V4, V5)
    rhoL = randn(ComplexF64, V1, V1)
    rhoR = randn(ComplexF64, V5, V5)' # test adjoint tensor
    H = randn(ComplexF64, V2 * V4, V2 * V4)
    @tensor HrA12[a, s1, s2, c] := rhoL[a, a'] * conj(A1[a', t1, b]) *
        A2[b, t2, c'] * rhoR[c', c] * H[s1, s2, t1, t2]

    @tensor HrA12array[a, s1, s2, c] := convert(Array, rhoL)[a, a'] *
        conj(convert(Array, A1)[a', t1, b]) * convert(Array, A2)[b, t2, c'] *
        convert(Array, rhoR)[c', c] * convert(Array, H)[s1, s2, t1, t2]

    @test HrA12array ≈ convert(Array, HrA12)
end

@testsuite :tensors "tensor product norm preservation" V -> begin
    V1, V2, V3, V4, V5 = V
    for T in (Float32, ComplexF64)
        t1 = rand(T, V1, V5')
        t2 = rand(T, V2 ⊗ V3, V4')
        t = @testinferred (t1 ⊗ t2)
        @test norm(t) ≈ norm(t1) * norm(t2)
    end
end

@testsuite :tensors "tensor product via conversion" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa Bosonic && hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    for T in (Float32, ComplexF64)
        t1 = rand(T, V1, V5')
        t2 = rand(T, V2 ⊗ V3, V4')
        t = @testinferred (t1 ⊗ t2)
        d1 = dim(codomain(t1))
        d2 = dim(codomain(t2))
        d3 = dim(domain(t1))
        d4 = dim(domain(t2))
        At = convert(Array, t)
        @test reshape(At, (d1, d2, d3, d4)) ≈
            reshape(convert(Array, t1), (d1, 1, d3, 1)) .*
            reshape(convert(Array, t2), (1, d2, 1, d4))
    end
end

@testsuite :tensors "tensor product via contraction" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa SymmetricBraiding || return nothing
    V1, V2, V3, V4, V5 = V
    for T in (Float32, ComplexF64)
        t1 = rand(T, V1, V5')
        t2 = rand(T, V2 ⊗ V3, V4')
        t = @testinferred (t1 ⊗ t2)
        @tensor t′[1 2 3; 4 5] := t1[1; 4] * t2[2 3; 5]
        @test t ≈ t′
    end
end

@testsuite :tensors "absorption" V -> begin
    V1, V2, V3, V4, V5 = V
    # absorbing small into large
    t1 = zeros((V1 ⊕ V1) ⊗ (V2 ⊕ V2), (V3 ⊗ (V4 ⊕ V4) ⊗ V5)')
    t2 = rand(V1 ⊗ V2, (V3 ⊗ V4 ⊗ V5)')
    t3 = @testinferred absorb(t1, t2)
    @test norm(t3) ≈ norm(t2)
    @test norm(t1) == 0
    t4 = @testinferred absorb!(t1, t2)
    @test t1 === t4
    @test t3 ≈ t4

    # absorbing large into small
    t1 = rand((V1 ⊕ V1) ⊗ (V2 ⊕ V2), (V3 ⊗ (V4 ⊕ V4) ⊗ V5)')
    t2 = zeros(V1 ⊗ V2, (V3 ⊗ V4 ⊗ V5)')
    t3 = @testinferred absorb(t2, t1)
    @test norm(t3) < norm(t1)
    @test norm(t2) == 0
    t4 = @testinferred absorb!(t2, t1)
    @test t2 === t4
    @test t3 ≈ t4
end

# linear algebra
#---------------

@testsuite :tensors "basic linear algebra" V -> begin
    I = sectortype(first(V))
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ← (V3 ⊗ V4 ⊗ V5)'
    for T in (Float32, ComplexF64)
        t = @testinferred rand(T, W)
        @test scalartype(t) == T
        @test space(t) == W
        @test space(t') == W'
        @test dim(t) == dim(space(t))
        @test codomain(t) == codomain(W)
        @test domain(t) == domain(W)
        # blocks for adjoint
        bs = @testinferred blocks(t')
        (c, b1), state = @testinferred Nothing iterate(bs)
        @test c == first(blocksectors(W'))
        next = @testinferred Nothing iterate(bs, state)
        b2 = @testinferred block(t', first(blocksectors(t')))
        @test b1 == b2
        @test eltype(bs) === Pair{typeof(c), typeof(b1)}
        @test typeof(b1) === TensorKit.blocktype(t')
        @test typeof(c) === sectortype(t)
        # linear algebra
        @test isa(@testinferred(norm(t)), real(T))
        @test norm(t)^2 ≈ dot(t, t)
        α = rand(T)
        @test norm(α * t) ≈ abs(α) * norm(t)
        @test norm(t + t, 2) ≈ 2 * norm(t, 2)
        @test norm(t + t, 1) ≈ 2 * norm(t, 1)
        @test norm(t + t, Inf) ≈ 2 * norm(t, Inf)
        p = 3 * rand(Float64)
        @test norm(t + t, p) ≈ 2 * norm(t, p)
        @test norm(t) ≈ norm(t')

        t2 = @testinferred rand!(similar(t))
        β = rand(T)
        @test @testinferred(dot(β * t2, α * t)) ≈ conj(β) * α * conj(dot(t, t2))
        @test dot(t2, t) ≈ conj(dot(t, t2))
        @test dot(t2, t) ≈ conj(dot(t2', t'))
        @test dot(t2, t) ≈ dot(t', t2')

        if UnitStyle(I) isa SimpleUnit || !isempty(blocksectors(V2 ⊗ V1))
            i1 = @testinferred(isomorphism(T, V1 ⊗ V2, V2 ⊗ V1)) # can't reverse fusion here when modules are involved
            i2 = @testinferred(isomorphism(Vector{T}, V2 ⊗ V1, V1 ⊗ V2))
            @test i1 * i2 == @testinferred(id(T, V1 ⊗ V2))
            @test i2 * i1 == @testinferred(id(Vector{T}, V2 ⊗ V1))
        end

        w = @testinferred isometry(T, V1 ⊗ (rightunitspace(V1) ⊕ rightunitspace(V1)), V1)
        @test dim(w) == 2 * dim(V1 ← V1)
        @test w' * w == id(Vector{T}, V1)
        @test w * w' == (w * w')^2
    end
end

@testsuite :tensors "linear algebra conversion" V -> begin
    I = sectortype(first(V))
    hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ⊗ V3 ← (V4 ⊗ V5)'
    for T in (Float32, ComplexF64)
        t = rand(T, W)
        t2 = @testinferred rand!(similar(t))
        @test norm(t, 2) ≈ norm(convert(Array, t), 2)
        @test dot(t2, t) ≈ dot(convert(Array, t2), convert(Array, t))
        α = rand(T)
        @test convert(Array, α * t) ≈ α * convert(Array, t)
        @test convert(Array, t + t) ≈ 2 * convert(Array, t)
    end
end

@testsuite :tensors "multiplication of isometries" V -> begin
    V1, V2, V3, V4, V5 = V
    W1 = V1 ⊗ V2 ⊗ V3
    W2 = (V4 ⊗ V5)'
    for T in (Float64, ComplexF64)
        t1 = randisometry(T, W1, W2)
        t2 = randisometry(T, W2 ← W2)
        @test isisometric(t1)
        @test isunitary(t2)
        P = t1 * t1'
        @test P * P ≈ P
    end
end

@testsuite :tensors "multiplication and inverse compatibility" V -> begin
    V1, V2, V3, V4, V5 = V
    W1 = V1 ⊗ V2 ⊗ V3
    W2 = (V4 ⊗ V5)'
    for T in (Float64, ComplexF64)
        t1 = rand(T, W1, W1)
        t2 = rand(T, W2 ← W2)
        t = rand(T, W1, W2)
        @test t1 * (t1 \ t) ≈ t
        @test (t / t2) * t2 ≈ t
        @test t1 \ one(t1) ≈ inv(t1)
        @test one(t1) / t1 ≈ pinv(t1)
        @test_throws SpaceMismatch inv(t)
        @test_throws SpaceMismatch t2 \ t
        @test_throws SpaceMismatch t / t1
        tp = pinv(t) * t
        @test tp ≈ tp * tp
    end
end

@testsuite :tensors "multiplication and inverse conversion" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa Bosonic && hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    W1 = V1 ⊗ V2 ⊗ V3
    W2 = (V4 ⊗ V5)'
    for T in (Float32, Float64, ComplexF32, ComplexF64)
        t1 = rand(T, W1 ← W1)
        t2 = rand(T, W2, W2)
        t = rand(T, W1 ← W2)
        d1 = dim(W1)
        d2 = dim(W2)
        At1 = reshape(convert(Array, t1), d1, d1)
        At2 = reshape(convert(Array, t2), d2, d2)
        At = reshape(convert(Array, t), d1, d2)
        @test reshape(convert(Array, t1 * t), d1, d2) ≈ At1 * At
        @test reshape(convert(Array, t1' * t), d1, d2) ≈ At1' * At
        @test reshape(convert(Array, t2 * t'), d2, d1) ≈ At2 * At'
        @test reshape(convert(Array, t2' * t'), d2, d1) ≈ At2' * At'

        @test reshape(convert(Array, inv(t1)), d1, d1) ≈ inv(At1)
        @test reshape(convert(Array, pinv(t)), d2, d1) ≈ pinv(At)

        if T == Float32 || T == ComplexF32
            continue
        end

        @test reshape(convert(Array, t1 \ t), d1, d2) ≈ At1 \ At
        @test reshape(convert(Array, t1' \ t), d1, d2) ≈ At1' \ At
        @test reshape(convert(Array, t2 \ t'), d2, d1) ≈ At2 \ At'
        @test reshape(convert(Array, t2' \ t'), d2, d1) ≈ At2' \ At'

        @test reshape(convert(Array, t2 / t), d2, d1) ≈ At2 / At
        @test reshape(convert(Array, t2' / t), d2, d1) ≈ At2' / At
        @test reshape(convert(Array, t1 / t'), d1, d2) ≈ At1 / At'
        @test reshape(convert(Array, t1' / t'), d1, d2) ≈ At1' / At'
    end
end

@testsuite :tensors "diag and diagm" V -> begin
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ← (V3 ⊗ V4 ⊗ V5)'
    t = randn(ComplexF64, W)
    d = LinearAlgebra.diag(t)
    D = LinearAlgebra.diagm(codomain(t), domain(t), d)
    @test LinearAlgebra.isdiag(D)
    @test LinearAlgebra.diag(D) == d
end

@testsuite :tensors "tensor functions" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa Bosonic && hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2
    for T in (Float64, ComplexF64)
        t = randn(T, W, W)
        s = dim(W)
        expt = @testinferred exp(t)
        @test reshape(convert(Array, expt), (s, s)) ≈
            exp(reshape(convert(Array, t), (s, s)))

        @test (@testinferred sqrt(t))^2 ≈ t
        @test reshape(convert(Array, sqrt(t^2)), (s, s)) ≈
            sqrt(reshape(convert(Array, t^2), (s, s)))

        @test exp(@testinferred log(expt)) ≈ expt
        @test reshape(convert(Array, log(expt)), (s, s)) ≈
            log(reshape(convert(Array, expt), (s, s)))

        @test (@testinferred cos(t))^2 + (@testinferred sin(t))^2 ≈ id(W)
        @test (@testinferred tan(t)) ≈ sin(t) / cos(t)
        @test (@testinferred cot(t)) ≈ cos(t) / sin(t)
        @test (@testinferred cosh(t))^2 - (@testinferred sinh(t))^2 ≈ id(W)
        @test (@testinferred tanh(t)) ≈ sinh(t) / cosh(t)
        @test (@testinferred coth(t)) ≈ cosh(t) / sinh(t)

        t1 = sin(t)
        @test sin(@testinferred asin(t1)) ≈ t1
        t2 = cos(t)
        @test cos(@testinferred acos(t2)) ≈ t2
        t3 = sinh(t)
        @test sinh(@testinferred asinh(t3)) ≈ t3
        t4 = cosh(t)
        @test cosh(@testinferred acosh(t4)) ≈ t4
        t5 = tan(t)
        @test tan(@testinferred atan(t5)) ≈ t5
        t6 = cot(t)
        @test cot(@testinferred acot(t6)) ≈ t6
        t7 = tanh(t)
        @test tanh(@testinferred atanh(t7)) ≈ t7
        t8 = coth(t)
        @test coth(@testinferred acoth(t8)) ≈ t8
        t = randn(T, W, V1) # not square
        for f in
            (
                cos, sin, tan, cot, cosh, sinh, tanh, coth, atan, acot, asinh,
                sqrt, log, asin, acos, acosh, atanh, acoth,
            )
            @test_throws SpaceMismatch f(t)
        end
    end
end

@testsuite :tensors "sylvester equation" V -> begin
    I = sectortype(first(V))
    V1, V2, V3, V4, V5 = V
    for T in (Float32, ComplexF64)
        tA = rand(T, V1 ⊗ V2, V1 ⊗ V2)
        tB = rand(T, (V3 ⊗ V4 ⊗ V5)', (V3 ⊗ V4 ⊗ V5)')
        tA = 3 // 2 * left_polar(tA)[1]
        tB = 1 // 5 * left_polar(tB)[1]
        tC = rand(T, V1 ⊗ V2, (V3 ⊗ V4 ⊗ V5)')
        t = @testinferred sylvester(tA, tB, tC)
        @test codomain(t) == V1 ⊗ V2
        @test domain(t) == (V3 ⊗ V4 ⊗ V5)'
        @test norm(tA * t + t * tB + tC) <
            (norm(tA) + norm(tB) + norm(tC)) * eps(real(T))^(2 / 3)
        if BraidingStyle(I) isa Bosonic && hasfusiontensor(I)
            matrix(x) = reshape(convert(Array, x), dim(codomain(x)), dim(domain(x)))
            @test matrix(t) ≈ sylvester(matrix(tA), matrix(tB), matrix(tC))
        end
    end
end

# index manipulations
#--------------------

@testsuite :tensors "trivial space insertion and removal" V -> begin
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ← (V3 ⊗ V4 ⊗ V5)'
    for T in (Float32, ComplexF64)
        t = @testinferred rand(T, W)
        t2 = @testinferred insertleftunit(t)
        @test t2 == @testinferred insertrightunit(t)
        @test space(t2) == insertleftunit(space(t))
        @test (@testinferred removeunit(t2, Val(numind(t2)))) == t
        t3 = @testinferred insertleftunit(t; copy = true)
        @test t3 == @testinferred insertrightunit(t; copy = true)
        @test (@testinferred removeunit(t3, Val(numind(t3)))) == t

        @test numind(t2) == numind(t) + 1
        @test scalartype(t2) === T
        @test t.data === t2.data

        @test t.data !== t3.data
        for (c, b) in blocks(t)
            @test b == block(t3, c)
        end

        t4 = @testinferred insertrightunit(t, Val(3); dual = true)
        @test numin(t4) == numin(t) + 1 && numout(t4) == numout(t)
        for (c, b) in blocks(t)
            @test b == block(t4, c)
        end
        @test (@testinferred removeunit(t4, Val(4))) == t

        t5 = @testinferred insertleftunit(t, Val(4); dual = true)
        @test numin(t5) == numin(t) + 1 && numout(t5) == numout(t)
        for (c, b) in blocks(t)
            @test b == block(t5, c)
        end
        @test (@testinferred removeunit(t5, Val(4))) == t
    end
end

@testsuite :tensors "permutations via inner product invariance" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa SymmetricBraiding || return nothing
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ⊗ V3 ⊗ V4 ⊗ V5
    t = rand(ComplexF64, W)
    t′ = randn!(similar(t))
    for k in 0:5
        for p in permutations(1:5)
            p1 = ntuple(n -> p[n], k)
            p2 = ntuple(n -> p[k + n], 5 - k)
            t2 = @testinferred permute(t, (p1, p2))
            @test norm(t2) ≈ norm(t)
            t2′ = permute(t′, (p1, p2))
            @test dot(t2′, t2) ≈ dot(t′, t) ≈ dot(transpose(t2′), transpose(t2))
        end

        t3 = @testinferred check_repartition(t, Val(k))
        @test norm(t3) ≈ norm(t)
        t3′ = @testinferred repartition!(similar(t3), t′)
        @test norm(t3′) ≈ norm(t′)
        @test dot(t′, t) ≈ dot(t3′, t3)
    end
end

@testsuite :tensors "permutations via conversion" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa Bosonic && hasfusiontensor(I) || return nothing
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ⊗ V3 ⊗ V4 ⊗ V5
    t = rand(ComplexF64, W)
    a = convert(Array, t)
    for k in 0:5
        for p in permutations(1:5)
            p1 = ntuple(n -> p[n], k)
            p2 = ntuple(n -> p[k + n], 5 - k)
            t2 = permute(t, (p1, p2))
            a2 = convert(Array, t2)
            @test a2 ≈ permutedims(a, (p1..., p2...))
            @test convert(Array, transpose(t2)) ≈
                permutedims(a2, (5, 4, 3, 2, 1))
        end

        t3 = repartition(t, k)
        a3 = convert(Array, t3)
        @test a3 ≈ permutedims(
            a, (ntuple(identity, k)..., reverse(ntuple(i -> i + k, 5 - k))...)
        )
    end
end

@testsuite :tensors "index flipping inverse" V -> begin # test flipping inverse
    I = sectortype(first(V))
    BraidingStyle(I) isa HasBraiding || return nothing
    V1, V2, V3, V4, V5 = V
    t = rand(ComplexF64, V1 ⊗ V2 ⊗ V3 ← (V4 ⊗ V5)')
    for i in 1:5
        @test t ≈ flip(flip(t, i), i; inv = true)
        @test t ≈ flip(flip(t, i; inv = true), i)
    end
end

@testsuite :tensors "index flipping explicit" V -> begin # test flipping via explicit flip
    I = sectortype(first(V))
    BraidingStyle(I) isa SymmetricBraiding || return nothing
    V1, V2, V3, V4, V5 = V
    t = rand(ComplexF64, V1 ⊗ V1' ← V1' ⊗ V1)
    F1 = unitary(flip(V1), V1)

    @tensor tf[a, b; c, d] := F1[a, a'] * t[a', b; c, d]
    @test flip(t, 1) ≈ tf
    @tensor tf[a, b; c, d] := conj(F1[b, b']) * t[a, b'; c, d]
    @test twist!(flip(t, 2), 2) ≈ tf
    @tensor tf[a, b; c, d] := F1[c, c'] * t[a, b; c', d]
    @test flip(t, 3) ≈ tf
    @tensor tf[a, b; c, d] := conj(F1[d, d']) * t[a, b; c, d']
    @test twist!(flip(t, 4), 4) ≈ tf
end

@testsuite :tensors "index flipping via contraction" V -> begin # test flipping via contraction
    I = sectortype(first(V))
    BraidingStyle(I) isa SymmetricBraiding || return nothing
    V1, V2, V3, V4, V5 = V
    t1 = rand(ComplexF64, V1 ⊗ V2 ⊗ V3 ← V4)
    t2 = rand(ComplexF64, V2' ⊗ V5 ← V4' ⊗ V1)
    @tensor ta[a, b] := t1[x, y, a, z] * t2[y, b, z, x]
    @tensor tb[a, b] := flip(t1, 1)[x, y, a, z] * flip(t2, 4)[y, b, z, x]
    @test ta ≈ tb
    @tensor tb[a, b] := flip(t1, (2, 4))[x, y, a, z] * flip(t2, (1, 3))[y, b, z, x]
    @test ta ≈ tb
    @tensor tb[a, b] := flip(t1, (1, 2, 4))[x, y, a, z] * flip(t2, (1, 3, 4))[y, b, z, x]
    @tensor tb[a, b] := flip(t1, (1, 3))[x, y, a, z] * flip(t2, (2, 4))[y, b, z, x]
    @test flip(ta, (1, 2)) ≈ tb
end

@testsuite :tensors "braid adjoint identity" V -> begin # Braid AdjointTensorMap: adjoint identity
    I = sectortype(first(V))
    (BraidingStyle(I) isa HasBraiding && !(BraidingStyle(I) isa SymmetricBraiding)) || return nothing
    V1, V2, V3, V4, V5 = V
    t = rand(ComplexF64, V1 ⊗ V2 ← V3)
    p = ((2,), (1, 3))
    levels = (1, 3, 2)
    t1 = copy(braid(t', p, levels))
    t2 = braid(copy(t'), p, levels)
    @test t1 ≈ t2
end

@testsuite :tensors "braid invalid levels" V -> begin # Braid invalid levels
    I = sectortype(first(V))
    (BraidingStyle(I) isa HasBraiding && !(BraidingStyle(I) isa SymmetricBraiding)) || return nothing
    t = rand(ComplexF64, V1 ⊗ V2 ← V3)
    p = ((2, 1), (3,))
    dupe_levels = (2, 2, 1) # level 2 is the duplicate
    bad_lengths = (1, 2)
    dupe_err_str = "ambiguous braid: two indices with equal level 2 have to cross"
    len_err_str = "length of levels should be $(numind(t)), got $(length(bad_lengths))"
    @test_throws ArgumentError(dupe_err_str) braid(t, p, dupe_levels) # duplicate levels
    @test_throws ArgumentError(len_err_str) braid(t, p, bad_lengths) # wrong length
    @test_throws ArgumentError(dupe_err_str) braid!(similar(t, permute(space(t), p)), t, p, dupe_levels)
end

# braiding tensor
#----------------

function _braiding_tensor_setup(V::NTuple{5, ElementarySpace})
    Vspace = first(V)
    I = sectortype(Vspace)
    t = randn(ComplexF64, Vspace ⊗ Vspace' ⊗ Vspace' ⊗ Vspace ← Vspace ⊗ Vspace')
    hasbraiding = BraidingStyle(I) isa HasBraiding
    return hasbraiding, Vspace, t
end

@testsuite :tensors "braiding tensor planaradd!" V -> begin
    hasbraiding, Vspace, _ = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    b = BraidingTensor(Vspace, Vspace')
    bb = TensorMap(b)
    # Cyclic rotations of the planar leg cycle (cod1, cod2, dom2, dom1).
    # Use transpose (F-symbols only) as reference, since permute requires SymmetricBraiding.
    # rotation 0 (identity)
    @planar t1[-1 -2; -3 -4] := b[-1 -2; -3 -4]
    @test t1 ≈ bb
    # rotation 1: single-tree cycle (4,1,2,3) → (p1=(2,4), p2=(1,3))
    @planar t2[-1 -2; -3 -4] := b[-3 -1; -4 -2]
    @test t2 ≈ transpose(bb, ((2, 4), (1, 3)))
    # rotation 2: single-tree cycle (3,4,1,2) → (p1=(4,3), p2=(2,1))
    @planar t3[-1 -2; -3 -4] := b[-4 -3; -2 -1]
    @test t3 ≈ transpose(bb, ((4, 3), (2, 1)))
    # rotation 3: single-tree cycle (2,3,4,1) → (p1=(3,1), p2=(4,2))
    @planar t4[-1 -2; -3 -4] := b[-2 -4; -1 -3]
    @test t4 ≈ transpose(bb, ((3, 1), (4, 2)))
    # adjoint BraidingTensor (rotation 0)
    ba = b'
    @planar t5[-1 -2; -3 -4] := ba[-1 -2; -3 -4]
    @test t5 ≈ TensorMap(ba)
end

@testsuite :tensors "braiding tensor left full contraction" V -> begin # τ as left factor, all legs contracted
    hasbraiding, Vspace, t = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    # BraidingTensor(V, V') on leading codomain indices
    ττ = TensorMap(BraidingTensor(Vspace, Vspace'))
    @planar t1[-1 -2 -3 -4; -5 -6] := τ[-1 -2; 1 2] * t[1 2 -3 -4; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := ττ[-1 -2; 1 2] * t[1 2 -3 -4; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := τ[2 1; -2 -1] * t[1 2 -3 -4; -5 -6]
    @planar t4[-1 -2 -3 -4; -5 -6] := τ'[-2 2; -1 1] * t[1 2 -3 -4; -5 -6]
    @test t1 ≈ braid(t, ((2, 1, 3, 4), (5, 6)), (1, 2, 3, 4, 5, 6))
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V', V') on inner codomain indices
    ττ = TensorMap(BraidingTensor(Vspace', Vspace'))
    @planar t1[-1 -2 -3 -4; -5 -6] := τ[-2 -3; 1 2] * t[-1 1 2 -4; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := ττ[-2 -3; 1 2] * t[-1 1 2 -4; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := τ[2 1; -3 -2] * t[-1 1 2 -4; -5 -6]
    @planar t4[-1 -2 -3 -4; -5 -6] := τ'[-3 2; -2 1] * t[-1 1 2 -4; -5 -6]
    @test t1 ≈ braid(t, ((1, 3, 2, 4), (5, 6)), (1, 2, 3, 4, 5, 6))
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V', V) on trailing codomain indices
    ττ = TensorMap(BraidingTensor(Vspace', Vspace))
    @planar t1[-1 -2 -3 -4; -5 -6] := τ[-3 -4; 1 2] * t[-1 -2 1 2; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := ττ[-3 -4; 1 2] * t[-1 -2 1 2; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := τ[2 1; -4 -3] * t[-1 -2 1 2; -5 -6]
    @planar t4[-1 -2 -3 -4; -5 -6] := τ'[-4 2; -3 1] * t[-1 -2 1 2; -5 -6]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4
end

@testsuite :tensors "braiding tensor left partial contraction" V -> begin # τ as left factor, mixed open legs
    hasbraiding, Vspace, t = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    # BraidingTensor(V', V) with mixed index pattern
    ττ = TensorMap(BraidingTensor(Vspace', Vspace))
    @planar t1[-1 -2 -3 -4; -5 -6] := τ[1 -2; 2 -3] * t[-1 1 2 -4; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := ττ[1 -2; 2 -3] * t[-1 1 2 -4; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := τ[-3 2; -2 1] * t[-1 1 2 -4; -5 -6]
    @planar t4[-1 -2 -3 -4; -5 -6] := τ'[-2 -3; 1 2] * t[-1 1 2 -4; -5 -6]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V, V') with mixed index pattern (inverse of previous)
    ττ = TensorMap(BraidingTensor(Vspace, Vspace'))
    @planar t1[-1 -2 -3 -4; -5 -6] := τ[-3 2; -2 1] * t[-1 1 2 -4; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := ττ[-3 2; -2 1] * t[-1 1 2 -4; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := τ[1 -2; 2 -3] * t[-1 1 2 -4; -5 -6]
    @planar t4[-1 -2 -3 -4; -5 -6] := τ'[2 1; -3 -2] * t[-1 1 2 -4; -5 -6]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V, V) with mixed index pattern
    ττ = TensorMap(BraidingTensor(Vspace, Vspace))
    @planar t1[-1 -2 -3 -4; -5 -6] := τ[2 1; -3 -2] * t[-1 1 2 -4; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := ττ[2 1; -3 -2] * t[-1 1 2 -4; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := τ[-2 -3; 1 2] * t[-1 1 2 -4; -5 -6]
    @planar t4[-1 -2 -3 -4; -5 -6] := τ'[1 -2; 2 -3] * t[-1 1 2 -4; -5 -6]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4
end

@testsuite :tensors "braiding tensor right full contraction" V -> begin # τ as right factor
    hasbraiding, Vspace, t = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    # BraidingTensor(V', V) on all domain indices
    ττ = TensorMap(BraidingTensor(Vspace', Vspace))
    @planar t1[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * τ[1 2; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * ττ[1 2; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * τ[-6 -5; 2 1]
    @planar t4[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * τ'[2 -6; 1 -5]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V, V') adjoint on all domain indices
    ττ = TensorMap(BraidingTensor(Vspace, Vspace'))
    @planar t1[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * τ'[1 2; -5 -6]
    @planar t2[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * ττ'[1 2; -5 -6]
    @planar t3[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * τ'[-6 -5; 2 1]
    @planar t4[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 -4; 1 2] * τ[2 -6; 1 -5]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V, V) with mixed domain legs
    ττ = TensorMap(BraidingTensor(Vspace, Vspace))
    @planar t1[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 1; -5 2] * τ[-4 -6; 1 2]
    @planar t2[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 1; -5 2] * ττ[-4 -6; 1 2]
    @planar t3[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 1; -5 2] * τ[2 1; -6 -4]
    @planar t4[-1 -2 -3 -4; -5 -6] := t[-1 -2 -3 1; -5 2] * τ'[-6 2; -4 1]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4
end

@testsuite :tensors "braiding tensor full contraction output" V -> begin
    hasbraiding, Vspace, t = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    # scalar output
    ττ = TensorMap(BraidingTensor(Vspace', Vspace))
    @planar t1[(); (-1, -2)] := τ[2 1; 3 4] * t[1 2 3 4; -1 -2]
    @planar t2[(); (-1, -2)] := ττ[2 1; 3 4] * t[1 2 3 4; -1 -2]
    @planar t3[(); (-1, -2)] := τ[4 3; 1 2] * t[1 2 3 4; -1 -2]
    @planar t4[(); (-1, -2)] := τ'[1 4; 2 3] * t[1 2 3 4; -1 -2]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # rank-1 output
    ττ = TensorMap(BraidingTensor(Vspace, Vspace))
    @planar t1[-1; -2] := τ[2 1; 3 4] * t[-1 1 2 3; -2 4]
    @planar t2[-1; -2] := ττ[2 1; 3 4] * t[-1 1 2 3; -2 4]
    @planar t3[-1; -2] := τ[4 3; 1 2] * t[-1 1 2 3; -2 4]
    @planar t4[-1; -2] := τ'[1 4; 2 3] * t[-1 1 2 3; -2 4]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # rank-2 output
    ττ = TensorMap(BraidingTensor(Vspace, Vspace'))
    @planar t1[-1 -2] := τ[2 1; 3 4] * t[-1 -2 1 2; 4 3]
    @planar t2[-1 -2] := ττ[2 1; 3 4] * t[-1 -2 1 2; 4 3]
    @planar t3[-1 -2] := τ[4 3; 1 2] * t[-1 -2 1 2; 4 3]
    @planar t4[-1 -2] := τ'[1 4; 2 3] * t[-1 -2 1 2; 4 3]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4
end

@testsuite :tensors "braiding tensor open codomain leg" V -> begin # τ with one open codomain leg
    hasbraiding, Vspace, t = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    # BraidingTensor(V, V') with one open codomain leg
    ττ = TensorMap(BraidingTensor(Vspace, Vspace'))
    @planar t1[-1 -2; -3 -4] := τ[-1 3; 1 2] * t[1 2 3 -2; -3 -4]
    @planar t2[-1 -2; -3 -4] := ττ[-1 3; 1 2] * t[1 2 3 -2; -3 -4]
    @planar t3[-1 -2; -3 -4] := τ[2 1; 3 -1] * t[1 2 3 -2; -3 -4]
    @planar t4[-1 -2; -3 -4] := τ'[3 2; -1 1] * t[1 2 3 -2; -3 -4]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V', V') adjoint with one open codomain leg
    ττ = TensorMap(BraidingTensor(Vspace', Vspace'))
    @planar t1[-1 -2; -3 -4] := τ'[-2 3; 1 2] * t[-1 1 2 3; -3 -4]
    @planar t2[-1 -2; -3 -4] := ττ'[-2 3; 1 2] * t[-1 1 2 3; -3 -4]
    @planar t3[-1 -2; -3 -4] := τ'[2 1; 3 -2] * t[-1 1 2 3; -3 -4]
    @planar t4[-1 -2; -3 -4] := τ[3 2; -2 1] * t[-1 1 2 3; -3 -4]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V', V) with one open codomain leg
    ττ = TensorMap(BraidingTensor(Vspace', Vspace))
    @planar t1[-1 -2 -3; -4] := τ[-3 3; 1 2] * t[-1 -2 1 2; -4 3]
    @planar t2[-1 -2 -3; -4] := ττ[-3 3; 1 2] * t[-1 -2 1 2; -4 3]
    @planar t3[-1 -2 -3; -4] := τ[2 1; 3 -3] * t[-1 -2 1 2; -4 3]
    @planar t4[-1 -2 -3; -4] := τ'[3 2; -3 1] * t[-1 -2 1 2; -4 3]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4
end

@testsuite :tensors "braiding tensor open domain leg" V -> begin # τ as right factor with open domain leg
    hasbraiding, Vspace, t = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    # BraidingTensor(V', V) as right factor with one open domain leg
    ττ = TensorMap(BraidingTensor(Vspace', Vspace))
    @planar t1[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * τ[1 2; -4 3]
    @planar t2[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * ττ[1 2; -4 3]
    @planar t3[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * τ[3 -4; 2 1]
    @planar t4[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * τ'[2 3; 1 -4]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4

    # BraidingTensor(V, V') adjoint as right factor with one open domain leg
    ττ = TensorMap(BraidingTensor(Vspace, Vspace'))
    @planar t1[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * τ'[1 2; -4 3]
    @planar t2[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * ττ'[1 2; -4 3]
    @planar t3[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * τ'[3 -4; 2 1]
    @planar t4[-1 -2 -3; -4] := t[-1 -2 -3 3; 1 2] * τ[2 3; 1 -4]
    @test t1 ≈ t2
    @test t1 ≈ t3
    @test t1 ≈ t4
end

@testsuite :tensors "contraction between braiding tensors" V -> begin # BraidingTensor × BraidingTensor
    hasbraiding, Vspace, t = _braiding_tensor_setup(V)
    hasbraiding || return nothing
    # b1 domain == b2 codomain == V⊗V', straight-through (planar) contraction
    b1 = BraidingTensor(Vspace, Vspace')   # space: V'⊗V ← V⊗V'
    b2 = BraidingTensor(Vspace', Vspace)   # space: V⊗V' ← V'⊗V
    bb1 = TensorMap(b1)
    bb2 = TensorMap(b2)
    @planar t1[-1 -2; -3 -4] := b1[-1 -2; 1 2] * b2[1 2; -3 -4]
    @planar t2[-1 -2; -3 -4] := bb1[-1 -2; 1 2] * bb2[1 2; -3 -4]
    @test t1 ≈ t2
end

@testsuite :tensors "braiding tensor properties" V -> begin
    I = sectortype(first(V))
    BraidingStyle(I) isa HasBraiding || return nothing
    V1, V2, V3, V4, V5 = V
    W = V1 ⊗ V2 ← V2 ⊗ V1
    t1 = @testinferred BraidingTensor(W)
    @test space(t1) == W
    @test codomain(t1) == codomain(W)
    @test domain(t1) == domain(W)
    @test scalartype(t1) == (isreal(sectortype(W)) ? Float64 : ComplexF64)
    @test storagetype(t1) == Vector{scalartype(t1)}
    t2 = @testinferred BraidingTensor{ComplexF64}(W)
    @test scalartype(t2) == ComplexF64
    @test storagetype(t2) == Vector{ComplexF64}

    W2 = reverse(codomain(W)) ← domain(W)
    @test_throws SpaceMismatch BraidingTensor(W2)

    @test adjoint(t1) isa BraidingTensor
    @test complex(t1) isa BraidingTensor
    @test scalartype(complex(t1)) <: Complex

    t3 = @inferred TensorMap(t2)
    t4 = braid(id(storagetype(t2), domain(t2)), ((2, 1), (3, 4)), (1, 2, 3, 4))
    @test t1 ≈ t4
    for (c, b) in blocks(t1)
        @test block(t1, c) ≈ b ≈ block(t3, c)
    end
    for (f1, f2) in fusiontrees(t1)
        @test t1[f1, f2] ≈ t3[f1, f2]
    end

    t5 = @inferred TensorMap(t2')
    t6 = braid(id(storagetype(t2), domain(t2')), ((2, 1), (3, 4)), (4, 3, 2, 1))
    @test t5 ≈ t6
    for (c, b) in blocks(t1')
        @test block(t1', c) ≈ b ≈ block(t5, c)
    end
    for (f1, f2) in fusiontrees(t1')
        @test t1'[f1, f2] ≈ t5[f1, f2]
    end
end

# hom space
#----------
@testsuite :tensors "hom space" V -> begin
    V1, V2, V3, V4, V5 = V
    W = HomSpace(V1 ⊗ V2, (V3 ⊗ V4 ⊗ V5)')
    @test W == ((V3 ⊗ V4 ⊗ V5)' → V1 ⊗ V2)
    @test W == (V1 ⊗ V2 ← (V3 ⊗ V4 ⊗ V5)')
    @test W' == (V1 ⊗ V2 → (V3 ⊗ V4 ⊗ V5)')
    @test codomain(W) == V1 ⊗ V2
    @test domain(W)' == V3 ⊗ V4 ⊗ V5
    @test eval_show(W) == W
    @test eval_show(typeof(W)) == typeof(W)
    @test spacetype(W) == typeof(V1)
    @test sectortype(W) == sectortype(V1)
    @test W[1] == V1
    @test W[2] == V2
    @test W[3] == V5
    @test W[4] == V4
    @test W[5] == V3
    @test all(W .== (V1, V2, V5, V4, V3))
    @test @testinferred(map(isdual, W)) == ntuple(i -> isdual(W[i]), length(W))
    @test @testinferred(hash(W)) == hash(deepcopy(W)) != hash(W')
    @test W == deepcopy(W)
    cod = codomain(W)
    dom = domain(W)
    @test (cod ← dom ⊗ rightunitspace(dom[3])) ==
        @testinferred(insertleftunit(W)) ==
        @testinferred(insertrightunit(W))
    @test (@testinferred removeunit(insertleftunit(W), Val(numind(W) + 1))) == W
    @test (cod ← dom ⊗ rightunitspace(dom[3])') ==
        @testinferred(insertleftunit(W; conj = true)) ==
        @testinferred(insertrightunit(W; conj = true))
    @test (leftunitspace(cod[1]) ⊗ cod ← dom) ==
        @testinferred(insertleftunit(W, Val(1))) ==
        @testinferred(insertrightunit(W, Val(0)))
    @test (cod ⊗ rightunitspace(cod[2]) ← dom) ==
        @testinferred(insertrightunit(W, Val(2)))
    @test (cod ← leftunitspace(dom[1]) ⊗ dom) ==
        @testinferred(insertleftunit(W, Val(3)))
    @test (@testinferred removeunit(insertleftunit(W, Val(3)), Val(3))) == W
    if UnitStyle(sectortype(W)) isa SimpleUnit
        @test @testinferred(insertrightunit(one(V1) ← V1, Val(0))) == (unitspace(V1) ← V1)
        @test_throws BoundsError insertleftunit(one(V1) ← V1, 0)
    else
        @test_throws ArgumentError insertrightunit(one(V1) ← V1, 0)
        @test_throws ArgumentError insertleftunit(one(V1) ← V1, 0)
    end
    @test (V1 ⊗ V2 ← V1 ⊗ V2) == @testinferred TensorKit.compose(W, W')
    @test W == @testinferred permute(W, ((1, 2), (3, 4, 5)))
    @test permute(W, ((2, 5, 4), (1, 3))) == (V2 ⊗ V3 ⊗ V4 ← V1' ⊗ V5') # cyclic permutation
end
