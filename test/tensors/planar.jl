using Test, TestExtras
using Adapt
using TensorKit
using TensorKit: type_repr
using TensorKit: PlanarTrivial, ℙ
using TensorKit: planaradd!, planartrace!, planarcontract!
using TensorKit: planar_contract_indices, SpaceMismatch
using TensorOperations

spacelist = default_spacelist(fast_tests)

for V in spacelist
    I = sectortype(first(V))
    Istr = type_repr(I)
    BraidingStyle(I) isa NoBraiding && continue
    @timedtestset "Braiding tensor with symmetry: $Istr" verbose = true begin
        W = V[1] ⊗ V[2] ← V[2] ⊗ V[1]
        t1 = @constinferred BraidingTensor(W)
        @test space(t1) == W
        @test codomain(t1) == codomain(W)
        @test domain(t1) == domain(W)
        @test scalartype(t1) == (isreal(sectortype(W)) ? Float64 : ComplexF64)
        @test storagetype(t1) == Vector{scalartype(t1)}
        t2 = @constinferred BraidingTensor{ComplexF64}(W)
        @test scalartype(t2) == ComplexF64
        @test storagetype(t2) == Vector{ComplexF64}
        t3 = @testinferred adapt(storagetype(t2), t1)
        @test storagetype(t3) == storagetype(t2)
        @test t3 == t2

        W2 = reverse(codomain(W)) ← domain(W)
        @test_throws SpaceMismatch BraidingTensor(W2)

        @test adjoint(t1) isa BraidingTensor
        @test complex(t1) isa BraidingTensor
        @test scalartype(complex(t1)) <: Complex

        t3 = @inferred TensorMap(t2)
        t4 = braid(id(storagetype(t2), domain(t2)), ((2, 1), (3, 4)), (1, 2, 3, 4))
        @test t1 ≈ t4
        for (c, b) in blocks(t1)
            @test block(t1, c) ≈ b ≈ block(t3, c)
        end
        for (f1, f2) in fusiontrees(t1)
            @test t1[f1, f2] ≈ t3[f1, f2]
        end

        t5 = @inferred TensorMap(t2')
        t6 = braid(id(storagetype(t2), domain(t2')), ((2, 1), (3, 4)), (4, 3, 2, 1))
        @test t5 ≈ t6
        for (c, b) in blocks(t1')
            @test block(t1', c) ≈ b ≈ block(t5, c)
        end
        for (f1, f2) in fusiontrees(t1')
            @test t1'[f1, f2] ≈ t5[f1, f2]
        end
    end
end

@testset "planar methods" verbose = true begin
    @testset "planaradd" begin
        A = randn(ℂ^2 ⊗ ℂ^3 ← ℂ^6 ⊗ ℂ^5 ⊗ ℂ^4)
        C = randn((ℂ^5)' ⊗ (ℂ^6)' ← ℂ^4 ⊗ (ℂ^3)' ⊗ (ℂ^2)')
        A′ = force_planar(A)
        C′ = force_planar(C)
        p = ((4, 3), (5, 2, 1))

        @test force_planar(tensoradd!(C, A, p, false, true, true)) ≈
            planaradd!(C′, A′, p, true, true)
    end

    @testset "planartrace" begin
        A = randn(ℂ^2 ⊗ ℂ^3 ← ℂ^2 ⊗ ℂ^5 ⊗ ℂ^4)
        C = randn((ℂ^5)' ⊗ ℂ^3 ← ℂ^4)
        A′ = force_planar(A)
        C′ = force_planar(C)
        p = ((4, 2), (5,))
        q = ((1,), (3,))

        @test force_planar(tensortrace!(C, A, p, q, false, true, true)) ≈
            planartrace!(C′, A′, p, q, true, true)
    end

    @testset "planarcontract" begin
        A = randn(ℂ^2 ⊗ ℂ^3 ← ℂ^2 ⊗ ℂ^5 ⊗ ℂ^4)
        B = randn(ℂ^2 ⊗ ℂ^4 ← ℂ^4 ⊗ ℂ^3)
        C = randn((ℂ^5)' ⊗ (ℂ^2)' ⊗ ℂ^2 ← (ℂ^2)' ⊗ ℂ^4)

        A′ = force_planar(A)
        B′ = force_planar(B)
        C′ = force_planar(C)

        pA = ((1, 3, 4), (5, 2))
        pB = ((2, 4), (1, 3))
        pAB = ((3, 2, 1), (4, 5))

        @test force_planar(tensorcontract!(C, A, pA, false, B, pB, false, pAB, true, true)) ≈
            planarcontract!(C′, A′, pA, B′, pB, pAB, true, true)

        # an output permutation that is not absorbed by the cyclic reordering
        pAB2 = ((2, 1), (3, 4, 5))
        D = randn((ℂ^2)' ⊗ ℂ^2 ← ℂ^5 ⊗ (ℂ^2)' ⊗ ℂ^4)
        D′ = force_planar(D)
        @test force_planar(tensorcontract!(D, A, pA, false, B, pB, false, pAB2, true, true)) ≈
            planarcontract!(D′, A′, pA, B′, pB, pAB2, true, true)

        # an output permutation that is not cyclic is not planar
        pAB3 = ((1, 2), (3, 4, 5))
        E′ = force_planar(randn(ℂ^2 ⊗ (ℂ^2)' ← ℂ^5 ⊗ (ℂ^2)' ⊗ ℂ^4))
        @test_throws ArgumentError planarcontract!(E′, A′, pA, B′, pB, pAB3, true, true)
    end

    @testset "planar_contract_indices" begin
        V1, V2, V3, V4, V5 = VIBM
        W = V1 ⊗ V2 ⊗ V3 ← (V4 ⊗ V5)'
        pA, pB = ((1,), (2, 3, 4, 5)), ((4, 5, 1, 2), (3,))
        pAB = ((1,), (2,))

        # the partitions of a planar contraction need not be planar by themselves
        @test_throws SpaceMismatch permute(W, pA)
        @test_throws SpaceMismatch permute(W', pB)

        pA′, pB′, pAB′ = @constinferred planar_contract_indices(W, pA, W', pB, pAB)
        @test permute(W, pA′) isa TensorKit.HomSpace
        @test permute(W', pB′) isa TensorKit.HomSpace
        @test TensorOperations.tensorcontract(W, pA′, false, W', pB′, false, pAB′) ==
            (V1 ← V1)

        # all indices contracted: the rotations are only fixed by the other factor
        pA0, pB0 = ((), (1, 2, 3, 4, 5)), ((3, 4, 5, 1, 2), ())
        pA0′, pB0′, pAB0′ = @constinferred planar_contract_indices(
            W, pA0, W', pB0, ((), ())
        )
        @test permute(W, pA0′) isa TensorKit.HomSpace
        @test permute(W', pB0′) isa TensorKit.HomSpace
        @test numind(
            TensorOperations.tensorcontract(W, pA0′, false, W', pB0′, false, pAB0′)
        ) == 0

        # not a planar contraction
        @test_throws ArgumentError planar_contract_indices(
            W, ((1,), (3, 2, 4, 5)), W', pB, pAB
        )
        @test_throws ArgumentError planar_contract_indices(
            W, ((2,), (1, 3, 4, 5)), W', pB, pAB
        )

        # the output permutation is remapped along with the reordered open indices
        WA = ℂ^2 ⊗ ℂ^3 ← ℂ^2 ⊗ ℂ^5 ⊗ ℂ^4
        WB = ℂ^2 ⊗ ℂ^4 ← ℂ^4 ⊗ ℂ^3
        pA2, pB2 = ((1, 3, 4), (5, 2)), ((2, 4), (1, 3))
        pA2′, pB2′, pAB2′ = planar_contract_indices(WA, pA2, WB, pB2, ((3, 2, 1), (4, 5)))
        @test (pA2′, pB2′) == (((4, 3, 1), (5, 2)), ((2, 4), (1, 3)))
        @test pAB2′ == ((1, 2, 3), (4, 5))
        @test last(planar_contract_indices(WA, pA2, WB, pB2, ((2, 1), (3, 4, 5)))) ==
            ((2, 3), (1, 4, 5))
    end
end

@testset "@planar" verbose = true begin
    T = ComplexF64

    @testset "backend and allocator insertion" begin
        # trailing arguments of every call in `ex` whose name is in `names`
        function planartrailing(ex, names, out = Any[])
            ex isa Expr || return out
            if Meta.isexpr(ex, :call) && ex.args[1] isa GlobalRef &&
                    ex.args[1].name in names
                push!(out, ex.args[end])
            end
            foreach(a -> planartrailing(a, names, out), ex.args)
            return out
        end

        ex = @macroexpand @planar backend = MarkerBackend() C[i; j] := A[i; k l] *
            τ[k l; m n] * B[m n; j]
        trailing = planartrailing(ex, (:planaradd!, :planartrace!, :planarcontract!))
        @test !isempty(trailing)
        @test all(==(:(MarkerBackend())), trailing)

        # an allocator implies a default backend, and both land on the planar calls
        ex = @macroexpand @planar allocator = MarkerAllocator() C[i; j] := A[i; k l] *
            τ[k l; m n] * B[m n; j]
        trailing = planartrailing(
            ex, (:planaradd!, :planartrace!, :planarcontract!, :planaralloc_contract)
        )
        @test !isempty(trailing)
        @test all(==(:(MarkerAllocator())), trailing)
        @test occursin("DefaultBackend", string(ex))

        alloc_trailing = planartrailing(ex, (:planaralloc_contract,))
        @test !isempty(alloc_trailing)
    end

    @testset "canonical index tuples" begin
        # the emitted partitions are planar, unlike the raw ones of the decomposition
        function planarindices(ex, out = Any[])
            ex isa Expr || return out
            if Meta.isexpr(ex, :call) && ex.args[1] isa GlobalRef &&
                    ex.args[1].name === :planarcontract!
                push!(out, (ex.args[4], ex.args[6], ex.args[7]))
            end
            foreach(a -> planarindices(a, out), ex.args)
            return out
        end
        ex = @macroexpand @planar ρ[a; b] := t[a c d; e f] * u[e f; b c d]
        @test planarindices(ex) ==
            [(((1,), (4, 5, 3, 2)), ((1, 2, 5, 4), (3,)), ((1,), (2,)))]
    end

    @testset "allocator is rewound" begin
        # A `BufferAllocator` hands out slices of a single buffer and reclaims them only
        # by rewinding its offset -- `tensorfree!` is a no-op for it. The temporaries a
        # block creates for intermediate results are released that way, so without a
        # checkpoint/reset pair around the block their space is never reclaimed. A buffer
        # that is not fully drained also never resizes itself, so it would stay pinned at
        # whatever size it first grew to, and every later temporary would fall back on the
        # garbage collector.
        for W in (ℂ^4, Vect[FermionParity](0 => 2, 1 => 2))
            A = rand(T, W ← W ⊗ W)
            B = rand(T, W ⊗ W ← W)
            @planar Cref[i; j] := A[i; k l] * τ[k l; m n] * B[m n; j]

            # three tensors, so the first contraction is an intermediate temporary
            buffer = TensorOperations.BufferAllocator(; sizehint = 1 << 16)
            @planar allocator = buffer C[i; j] := A[i; k l] * τ[k l; m n] * B[m n; j]
            @test isempty(buffer)
            @test C ≈ Cref

            # the result must not live in the buffer: the next block hands out the same
            # memory again, and `C` has to survive that
            @planar allocator = buffer C2[i; j] := A[i; k l] * τ[k l; m n] * B[m n; j]
            @test isempty(buffer)
            @test C ≈ Cref
            @test C2 ≈ Cref
        end
    end

    @testset "contractcheck" begin
        V = ℂ^2
        A = rand(T, V ⊗ V ← V)
        B = rand(T, V ⊗ V ← V')
        @tensor C1[i j; k l] := A[i j; m] * B[k l; m]
        @tensor contractcheck = true C2[i j; k l] := A[i j; m] * B[k l; m]
        @test C1 ≈ C2
        B2 = rand(T, V ⊗ V ← V) # wrong duality for third space
        @test_throws SpaceMismatch("incompatible spaces for m: $V ≠ $(V')") begin
            @tensor contractcheck = true C3[i j; k l] := A[i j; m] * B2[k l; m]
        end

        A = rand(T, V ← V ⊗ V)
        B = rand(T, V ⊗ V ← V)
        @planar C1[i; j] := A[i; k l] * τ[k l; m n] * B[m n; j]
        @planar contractcheck = true C2[i; j] := A[i; k l] * τ[k l; m n] * B[m n; j]
        @test C1 ≈ C2
        @test_throws SpaceMismatch("incompatible spaces for m: $V ≠ $(V')") begin
            @planar contractcheck = true C3[i; j] := A[i; k l] * τ[k l; m n] * B[n j; m]
        end
    end

    @testset "MPS networks" begin
        P = ℂ^2
        Vmps = ℂ^12
        Vmpo = ℂ^4

        # ∂AC
        # -------
        x = randn(T, Vmps ⊗ P ← Vmps)
        O = randn(T, Vmpo ⊗ P ← P ⊗ Vmpo)
        GL = randn(T, Vmps ⊗ Vmpo' ← Vmps)
        GR = randn(T, Vmps ⊗ Vmpo ← Vmps)

        x′ = force_planar(x)
        O′ = force_planar(O)
        GL′ = force_planar(GL)
        GR′ = force_planar(GR)

        for alloc in
            (TensorOperations.DefaultAllocator(), TensorOperations.ManualAllocator())
            @tensor allocator = alloc y[-1 -2; -3] := GL[-1 2; 1] * x[1 3; 4] *
                O[2 -2; 3 5] * GR[4 5; -3]
            @planar allocator = alloc y′[-1 -2; -3] := GL′[-1 2; 1] * x′[1 3; 4] *
                O′[2 -2; 3 5] * GR′[4 5; -3]
            @test force_planar(y) ≈ y′
        end

        # ∂AC2
        # -------
        x2 = randn(T, Vmps ⊗ P ← Vmps ⊗ P')
        x2′ = force_planar(x2)
        @tensor contractcheck = true y2[-1 -2; -3 -4] := GL[-1 7; 6] * x2[6 5; 1 3] *
            O[7 -2; 5 4] * O[4 -4; 3 2] *
            GR[1 2; -3]
        @planar y2′[-1 -2; -3 -4] := GL′[-1 7; 6] * x2′[6 5; 1 3] * O′[7 -2; 5 4] *
            O′[4 -4; 3 2] * GR′[1 2; -3]
        @test force_planar(y2) ≈ y2′

        # transfer matrix
        # ----------------
        v = randn(T, Vmps ← Vmps)
        v′ = force_planar(v)
        @tensor ρ[-1; -2] := x[-1 2; 1] * conj(x[-2 2; 3]) * v[1; 3]
        @planar ρ′[-1; -2] := x′[-1 2; 1] * conj(x′[-2 2; 3]) * v′[1; 3]
        @test force_planar(ρ) ≈ ρ′

        @tensor ρ2[-1 -2; -3] := GL[1 -2; 3] * x[3 2; -3] * conj(x[1 2; -1])
        @plansor ρ3[-1 -2; -3] := GL[1 2; 4] * x[4 5; -3] * τ[2 3; 5 -2] * conj(x[1 3; -1])
        @planar ρ2′[-1 -2; -3] := GL′[1 2; 4] * x′[4 5; -3] * τ[2 3; 5 -2] *
            conj(x′[1 3; -1])
        @test force_planar(ρ2) ≈ ρ2′
        @test ρ2 ≈ ρ3

        # Periodic boundary conditions
        # ----------------------------
        f1 = isomorphism(storagetype(O), fuse(Vmpo^3), Vmpo ⊗ Vmpo' ⊗ Vmpo)
        f2 = isomorphism(storagetype(O), fuse(Vmpo^3), Vmpo ⊗ Vmpo' ⊗ Vmpo)
        f1′ = force_planar(f1)
        f2′ = force_planar(f2)
        @tensor O_periodic1[-1 -2; -3 -4] := O[1 -2; -3 2] * f1[-1; 1 3 4] *
            conj(f2[-4; 2 3 4])
        @plansor O_periodic2[-1 -2; -3 -4] := O[1 2; -3 6] * f1[-1; 1 3 5] *
            conj(f2[-4; 6 7 8]) * τ[2 3; 7 4] *
            τ[4 5; 8 -2]
        @planar O_periodic′[-1 -2; -3 -4] := O′[1 2; -3 6] * f1′[-1; 1 3 5] *
            conj(f2′[-4; 6 7 8]) * τ[2 3; 7 4] *
            τ[4 5; 8 -2]
        @test O_periodic1 ≈ O_periodic2
        @test force_planar(O_periodic1) ≈ O_periodic′
    end

    @testset "MERA networks" begin
        Vmera = ℂ^2

        u = randn(T, Vmera ⊗ Vmera ← Vmera ⊗ Vmera)
        w = randn(T, Vmera ⊗ Vmera ← Vmera)
        ρ = randn(T, Vmera ⊗ Vmera ⊗ Vmera ← Vmera ⊗ Vmera ⊗ Vmera)
        h = randn(T, Vmera ⊗ Vmera ⊗ Vmera ← Vmera ⊗ Vmera ⊗ Vmera)

        u′ = force_planar(u)
        w′ = force_planar(w)
        ρ′ = force_planar(ρ)
        h′ = force_planar(h)

        for alloc in
            (TensorOperations.DefaultAllocator(), TensorOperations.ManualAllocator())
            @tensor allocator = alloc begin
                C = (
                    (
                        (
                            (
                                (
                                    ((h[9 3 4; 5 1 2] * u[1 2; 7 12]) * conj(u[3 4; 11 13])) *
                                        (u[8 5; 15 6] * w[6 7; 19])
                                ) *
                                    (conj(u[8 9; 17 10]) * conj(w[10 11; 22]))
                            ) *
                                ((w[12 14; 20] * conj(w[13 14; 23])) * ρ[18 19 20; 21 22 23])
                        ) *
                            w[16 15; 18]
                    ) * conj(w[16 17; 21])
                )
            end
            @planar allocator = alloc begin
                C′ = (
                    (
                        (
                            (
                                (
                                    ((h′[9 3 4; 5 1 2] * u′[1 2; 7 12]) * conj(u′[3 4; 11 13])) *
                                        (u′[8 5; 15 6] * w′[6 7; 19])
                                ) *
                                    (conj(u′[8 9; 17 10]) * conj(w′[10 11; 22]))
                            ) *
                                ((w′[12 14; 20] * conj(w′[13 14; 23])) * ρ′[18 19 20; 21 22 23])
                        ) *
                            w′[16 15; 18]
                    ) * conj(w′[16 17; 21])
                )
            end
            @test C ≈ C′
        end
    end

    @testset "Issue 93" begin
        T = Float64
        V1 = ℂ^2
        V2 = ℂ^3
        t1 = rand(T, V1 ← V2)
        t2 = rand(T, V2 ← V1)

        tr1 = @planar opt = true t1[a; b] * t2[b; a] / 2
        tr2 = @planar opt = true t1[d; a] * t2[b; c] * 1 / 2 * τ[c b; a d]
        tr3 = @planar opt = true t1[d; a] * t2[b; c] * τ[a c; d b] / 2
        tr4 = @planar opt = true t1[f; a] * 1 / 2 * t2[c; d] * τ[d b; c e] * τ[e b; a f]
        tr5 = @planar opt = true t1[f; a] * t2[c; d] / 2 * τ[d b; c e] * τ[a e; f b]
        tr6 = @planar opt = true t1[f; a] * t2[c; d] * τ[c d; e b] / 2 * τ[e b; a f]
        tr7 = @planar opt = true t1[f; a] * t2[c; d] * (τ[c d; e b] * τ[a e; f b] / 2)

        @test tr1 ≈ tr2 ≈ tr3 ≈ tr4 ≈ tr5 ≈ tr6 ≈ tr7

        tr1 = @plansor opt = true t1[a; b] * t2[b; a] / 2
        tr2 = @plansor opt = true t1[d; a] * t2[b; c] * 1 / 2 * τ[c b; a d]
        tr3 = @plansor opt = true t1[d; a] * t2[b; c] * τ[a c; d b] / 2
        tr4 = @plansor opt = true t1[f; a] * 1 / 2 * t2[c; d] * τ[d b; c e] * τ[e b; a f]
        tr5 = @plansor opt = true t1[f; a] * t2[c; d] / 2 * τ[d b; c e] * τ[a e; f b]
        tr6 = @plansor opt = true t1[f; a] * t2[c; d] * τ[c d; e b] / 2 * τ[e b; a f]
        tr7 = @plansor opt = true t1[f; a] * t2[c; d] * (τ[c d; e b] * τ[a e; f b] / 2)

        @test tr1 ≈ tr2 ≈ tr3 ≈ tr4 ≈ tr5 ≈ tr6 ≈ tr7
    end
    @testset "Issue 262" begin
        V = ℂ^2
        A = rand(T, V ← V)
        B = rand(T, V ← V')
        C = rand(T, V' ← V)
        @planar D1[i; j] := A[i; j] + B[i; k] * C[k; j]
        @planar D2[i; j] := B[i; k] * C[k; j] + A[i; j]
        @test D1 ≈ D2
    end
end
