using Test, TestExtras
using TensorKit
using TensorKit: type_repr, SectorDict
using TensorOperations
using ChainRulesCore
using ChainRulesTestUtils
using FiniteDifferences: FiniteDifferences, central_fdm, forward_fdm
using Random
using LinearAlgebra
using Zygote
using MatrixAlgebraKit

# Tests
# -----

ChainRulesTestUtils.test_method_tables()

spacelist = ad_spacelist(fast_tests)

for V in spacelist
    I = sectortype(eltype(V))
    Istr = type_repr(I)
    eltypes = isreal(sectortype(eltype(V))) ? (Float64, ComplexF64) : (ComplexF64,)
    hasbraiding = BraidingStyle(sectortype(eltype(V))) isa HasBraiding
    symmetricbraiding = BraidingStyle(sectortype(eltype(V))) isa SymmetricBraiding
    println("---------------------------------------")
    println("Auto-diff with symmetry: $Istr")
    println("---------------------------------------")
    @timedtestset "Chainrules for linear algebra operations with symmetry $Istr" verbose = true begin
        V1, V2, V3, V4, V5 = V
        W = V1 ⊗ V2
        @timedtestset "Basic utility" begin
            T1 = randn(Float64, V[1] ⊗ V[2] ← (V[3] ⊗ V[4] ⊗ V[5])')
            T2 = randn(ComplexF64, V[1] ⊗ V[2] ← (V[3] ⊗ V[4] ⊗ V[5])')

            P1 = ProjectTo(T1)
            @test P1(T1) == T1
            @test P1(T2) == real(T2)

            test_rrule(copy, T1)
            test_rrule(copy, T2)
            test_rrule(TensorKit.copy_oftype, T1, ComplexF64)
            if symmetricbraiding
                test_rrule(convert, Array, T1)
                test_rrule(
                    TensorMap, convert(Array, T1), codomain(T1), domain(T1);
                    fkwargs = (; tol = Inf)
                )
            end

            test_rrule(Base.getproperty, T1, :data)
            test_rrule(TensorMap{scalartype(T1)}, T1.data, T1.space)
            test_rrule(Base.getproperty, T2, :data)
            test_rrule(TensorMap{scalartype(T2)}, T2.data, T2.space)
        end

        @timedtestset "Basic utility (DiagonalTensor)" begin
            for v in V
                rdim = reduceddim(v)
                D1 = DiagonalTensorMap(randn(rdim), v)
                D2 = DiagonalTensorMap(randn(rdim), v)
                D = D1 + im * D2
                T1 = TensorMap(D1)
                T2 = TensorMap(D2)
                T = T1 + im * T2

                # real -> real
                P1 = ProjectTo(D1)
                @test P1(D1) == D1
                @test P1(T1) == D1

                # complex -> complex
                P2 = ProjectTo(D)
                @test P2(D) == D
                @test P2(T) == D

                # real -> complex
                @test P2(D1) == D1 + 0 * im * D1
                @test P2(T1) == D1 + 0 * im * D1

                # complex -> real
                @test P1(D) == D1
                @test P1(T) == D1

                test_rrule(DiagonalTensorMap, D1.data, D1.domain)
                test_rrule(DiagonalTensorMap, D.data, D.domain)
                test_rrule(Base.getproperty, D, :data)
                test_rrule(Base.getproperty, D1, :data)

                test_rrule(DiagonalTensorMap, rand!(T1))
                test_rrule(DiagonalTensorMap, randn!(T))
            end
        end

        @timedtestset "Basic Linear Algebra with scalartype $T" for T in eltypes
            A = randn(T, V[1] ⊗ V[2] ← (V[3] ⊗ V[4] ⊗ V[5])')
            B = randn(T, space(A))

            test_rrule(real, A)
            test_rrule(imag, A)

            test_rrule(+, A, B)
            test_rrule(-, A)
            test_rrule(-, A, B)

            α = randn(T)
            test_rrule(*, α, A)
            test_rrule(*, A, α)

            C = randn(T, domain(A), codomain(A))
            test_rrule(*, A, C)

            test_rrule(transpose, A, ((2, 5, 4), (1, 3)))
            symmetricbraiding && test_rrule(permute, A, ((1, 3, 2), (5, 4)))

            # the rrules must accept every keyword the primal accepts: `repartition`
            # forwards its own `backend`/`allocator` defaults on to `transpose`, so a
            # `copy`-only rrule signature breaks AD for callers that pass no keywords at all
            bakwargs = (;
                backend = TensorOperations.DefaultBackend(),
                allocator = TensorOperations.DefaultAllocator(),
            )
            test_rrule(transpose, A, ((2, 5, 4), (1, 3)); fkwargs = bakwargs)
            symmetricbraiding &&
                test_rrule(permute, A, ((1, 3, 2), (5, 4)); fkwargs = bakwargs)

            # `repartition` has no rrule of its own: it is differentiated by AD-ing through
            # its body down to the `transpose` rrule, so it has to be tested end-to-end
            # rather than with `test_rrule`
            for k in 0:numind(A)
                test_ad_rrule(repartition, A, k)
            end
            # also cover the explicit-`N₂` arity and the `copy` keyword
            test_ad_rrule(repartition, A, 2, numind(A) - 2)
            test_ad_rrule(repartition, A, 2; fkwargs = (; copy = true))

            hasbraiding && test_rrule(twist, A, 1)
            hasbraiding && test_rrule(twist, A, [1, 3])

            hasbraiding && test_rrule(flip, A, 1)
            hasbraiding && test_rrule(flip, A, [1, 3, 4])

            D = randn(T, V[1] ⊗ V[2] ← V[1] ⊗ V[2])
            E = randn(T, V[3] ← V[3])
            symmetricbraiding && test_rrule(⊗, D, E)
        end

        @timedtestset "Linear Algebra part II with scalartype $T" for T in eltypes
            atol = default_tol(T)
            rtol = default_tol(T)
            for i in 1:3
                E = randn(T, ⊗(V[1:i]...) ← ⊗(V[1:i]...))
                test_rrule(LinearAlgebra.tr, E; atol, rtol)
                test_rrule(exp, E; check_inferred = false, atol, rtol)
                test_rrule(inv, E; atol, rtol)
            end

            A = randn(T, V[1] ⊗ V[2] ← (V[3] ⊗ V[4] ⊗ V[5])')
            test_rrule(LinearAlgebra.adjoint, A; atol, rtol)
            test_rrule(LinearAlgebra.norm, A, 2; atol, rtol)

            B = randn(T, space(A))
            test_rrule(LinearAlgebra.dot, A, B; atol, rtol)
        end

        @timedtestset "Matrix functions ($T)" for T in eltypes
            atol = default_tol(T)
            rtol = default_tol(T)
            for f in (sqrt, exp)
                check_inferred = false # !(T <: Real) # not type-stable for real functions
                t1 = randn(T, V[1] ← V[1])
                t2 = randn(T, V[2] ← V[2])
                d = DiagonalTensorMap{T}(undef, V[1])
                d2 = DiagonalTensorMap{T}(undef, V[1])
                d3 = DiagonalTensorMap{T}(undef, V[1])
                if (T <: Real && f === sqrt)
                    # ensuring no square root of negative numbers
                    randexp!(d.data)
                    d.data .+= 5
                    randexp!(d2.data)
                    d2.data .+= 5
                    randexp!(d3.data)
                    d3.data .+= 5
                else
                    randn!(d.data)
                    randn!(d2.data)
                    randn!(d3.data)
                end

                test_rrule(f, t1; rrule_f = Zygote.rrule_via_ad, check_inferred, atol, rtol)
                test_rrule(f, t2; rrule_f = Zygote.rrule_via_ad, check_inferred, atol, rtol)
                test_rrule(f, d ⊢ d2; check_inferred, output_tangent = d3, atol, rtol)
            end
        end
    end
end
