"""
    struct GeneralSpace{ğ•œ} <: ElementarySpace{ğ•œ}

A finite-dimensional space over an arbitrary field `ğ•œ` without additional structure. It is
thus characterized by its dimension, and whether or not it is the dual and/or conjugate
space. For a real field `ğ•œ`, the space and its conjugate are the same.
"""
struct GeneralSpace{ğ•œ} <: ElementarySpace{ğ•œ}
    d::Int
    dual::Bool
    conj::Bool
    function GeneralSpace{ğ•œ}(d::Int, dual::Bool, conj::Bool) where {ğ•œ}
        d >= 0 ||
            throw(ArgumentError("Dimension of a vector space should be bigger than zero"))
        if ğ•œ isa Field
            new{ğ•œ}(Int(d), dual, (ğ•œ âŠ† â„) ? false : conj)
        else
            throw(ArgumentError("Unrecognised scalar field: $ğ•œ"))
        end
    end
end
GeneralSpace{ğ•œ}(d::Int; dual::Bool = false, conj::Bool = false) where {ğ•œ} =
    GeneralSpace{ğ•œ}(d, dual, conj)

dim(V::GeneralSpace) = V.d
isdual(V::GeneralSpace) = V.dual
isconj(V::GeneralSpace) = V.conj

Base.axes(V::GeneralSpace) = Base.OneTo(dim(V))

dual(V::GeneralSpace{ğ•œ}) where {ğ•œ} =
    GeneralSpace{ğ•œ}(dim(V), !isdual(V), isconj(V))
Base.conj(V::GeneralSpace{ğ•œ}) where {ğ•œ} =
    GeneralSpace{ğ•œ}(dim(V), isdual(V), !isconj(V))

function Base.show(io::IO, V::GeneralSpace{ğ•œ}) where {ğ•œ}
    if isconj(V)
        print(io, "conj(")
    end
    print(io, "GeneralSpace{", ğ•œ, "}(", dim(V), ")")
    if isdual(V)
        print(io, "'")
    end
    if isconj(V)
        print(io, ")")
    end
end
Base.show(io::IO, ::Type{GeneralSpace}) = print(io, "GeneralSpace")
