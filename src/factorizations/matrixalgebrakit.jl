# Algorithm selection
# -------------------
for f in
    [
        :svd_compact, :svd_full, :svd_vals,
        :qr_compact, :qr_full, :qr_null,
        :lq_compact, :lq_full, :lq_null,
        :eig_full, :eig_vals, :eigh_full, :eigh_vals,
        :left_polar, :right_polar,
        :project_hermitian, :project_antihermitian, :project_isometric,
        :exponential,
    ]
    f! = Symbol(f, :!)
    @eval function MAK.default_algorithm(::typeof($f!), ::Type{T}; kwargs...) where {T <: AbstractTensorMap}
        return MAK.default_algorithm($f!, blocktype(T); kwargs...)
    end
    @eval function MAK.copy_input(::typeof($f), t::AbstractTensorMap)
        return @timeit_debug GLOBAL_TIMER "alloc: copy_input" copy_oftype(
            t, factorisation_scalartype($f, t)
        )
    end
end

MAK.default_algorithm(::typeof(exponential!), ::Type{Tuple{E, T}}; kwargs...) where {E <: Number, T <: AbstractTensorMap} =
    MAK.default_algorithm(exponential!, blocktype(T); kwargs...)
MAK.copy_input(::typeof(exponential), (τ, t)::Tuple{E, T}) where {E <: Number, T <: AbstractTensorMap} =
    (τ, copy_oftype(t, (E <: Complex ? complex : identity)(factorisation_scalartype(exponential, t))))

_select_truncation(f, ::AbstractTensorMap, trunc::TruncationStrategy) = trunc
function _select_truncation(::typeof(left_null!), ::AbstractTensorMap, trunc::NamedTuple)
    return MAK.null_truncation_strategy(; trunc...)
end

# Generic Implementations
# -----------------------
for f! in (
        :qr_compact!, :qr_full!, :lq_compact!, :lq_full!,
        :eig_full!, :eigh_full!, :svd_compact!, :svd_full!,
        :left_polar!, :right_polar!,
    )
    @eval function MAK.$f!(t::AbstractTensorMap, F, alg::AbstractAlgorithm)
        $(f! in (:eig_full!, :eigh_full!) && :(LinearAlgebra.checksquare(t)))
        @timeit_debug GLOBAL_TIMER $(string(f!)) begin
            foreachblock(t, F...) do _, (tblock, Fblocks...)
                @timeit_debug GLOBAL_TIMER "dense: lapack" begin
                    Fblocks′ = $f!(tblock, Fblocks, alg)
                    # deal with the case where the output is not in-place
                    for (b′, b) in zip(Fblocks′, Fblocks)
                        b === b′ || copy!(b, b′)
                    end
                end
                return nothing
            end
        end
        return F
    end
end

# Handle these separately because single output instead of tuple
for f! in (
        :qr_null!, :lq_null!,
        :svd_vals!, :eig_vals!, :eigh_vals!,
        :project_hermitian!, :project_antihermitian!, :project_isometric!,
        :exponential!,
    )
    @eval function MAK.$f!(t::AbstractTensorMap, N, alg::AbstractAlgorithm)
        $(f! in (:eig_vals!, :eigh_vals!, :project_hermitian!, :project_antihermitian!, :exponential!) && :(LinearAlgebra.checksquare(t)))
        @timeit_debug GLOBAL_TIMER $(string(f!)) begin
            foreachblock(t, N) do _, (tblock, Nblock)
                @timeit_debug GLOBAL_TIMER "dense: lapack" begin
                    Nblock′ = $f!(tblock, Nblock, alg)
                    # deal with the case where the output is not the same as the input
                    Nblock === Nblock′ || copy!(Nblock, Nblock′)
                end
                return nothing
            end
        end
        return N
    end
end

# Exponential with Tuple
function MAK.exponential!((τ, t)::Tuple{E, T}, N, alg::AbstractAlgorithm) where {E <: Number, T <: AbstractTensorMap}
    LinearAlgebra.checksquare(t)
    @timeit_debug GLOBAL_TIMER "exponential!" begin
        foreachblock(t, N) do _, (tblock, Nblock)
            @timeit_debug GLOBAL_TIMER "dense: lapack" begin
                Nblock′ = exponential!((τ, tblock), Nblock, alg)
                # deal with the case where the output is not the same as the input
                Nblock === Nblock′ || copy!(Nblock, Nblock′)
            end
            return nothing
        end
    end
    return N
end


MAK.zero!(t::AbstractTensorMap) = zerovector!(t)

# Default algorithm
# -----------------
for f in [
        :lq_full, :lq_compact, :lq_null,
        :qr_full, :qr_compact, :qr_null,
        :schur_full, :schur_vals,
        :eig_full, :eig_vals, :eig_trunc, :eig_trunc_no_error,
        :eigh_full, :eigh_vals, :eigh_trunc, :eigh_trunc_no_error,
        :svd_full, :svd_compact, :svd_trunc, :svd_trunc_no_error, :svd_vals,
        :left_polar, :right_polar,
        :left_orth, :right_orth, :left_null, :right_null,
        :project_hermitian, :project_antihermitian, :project_isometric,
        :exponential,
    ]
    f! = Symbol(f, :!)
    @eval MAK.$f!(t::AbstractTensorMap, alg::DefaultAlgorithm) =
        MAK.$f!(t, MAK.select_algorithm(MAK.$f!, t, nothing; alg.kwargs...))
    @eval MAK.$f!(t::AbstractTensorMap, out, alg::DefaultAlgorithm) =
        MAK.$f!(t, out, MAK.select_algorithm(MAK.$f!, t, nothing; alg.kwargs...))

    # disambiguate
    @eval MAK.$f!(t::AdjointTensorMap, alg::DefaultAlgorithm) =
        MAK.$f!(t, MAK.select_algorithm(MAK.$f!, t, nothing; alg.kwargs...))
    @eval MAK.$f!(t::AdjointTensorMap, out, alg::DefaultAlgorithm) =
        MAK.$f!(t, out, MAK.select_algorithm(MAK.$f!, t, nothing; alg.kwargs...))

    @eval MAK.$f!(t::DiagonalTensorMap, alg::DefaultAlgorithm) =
        MAK.$f!(t, MAK.select_algorithm(MAK.$f!, t, nothing; alg.kwargs...))
    @eval MAK.$f!(t::DiagonalTensorMap, out, alg::DefaultAlgorithm) =
        MAK.$f!(t, out, MAK.select_algorithm(MAK.$f!, t, nothing; alg.kwargs...))
end

# resolve `DefaultAlgorithm` for the tuple form at the tensor level, mirroring the loop below
MAK.exponential!((τ, t)::Tuple{E, T}, alg::DefaultAlgorithm) where {E <: Number, T <: AbstractTensorMap} =
    MAK.exponential!((τ, t), MAK.select_algorithm(exponential!, t, nothing; alg.kwargs...))
MAK.exponential!((τ, t)::Tuple{E, T}, out, alg::DefaultAlgorithm) where {E <: Number, T <: AbstractTensorMap} =
    MAK.exponential!((τ, t), out, MAK.select_algorithm(exponential!, t, nothing; alg.kwargs...))


# Singular value decomposition
# ----------------------------
function MAK.initialize_output(::typeof(svd_full!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_cod = fuse(codomain(t))
        V_dom = fuse(domain(t))
        U = similar(t, codomain(t) ← V_cod)
        S = similar(t, real(scalartype(t)), V_cod ← V_dom)
        Vᴴ = similar(t, V_dom ← domain(t))
        return U, S, Vᴴ
    end
end

function MAK.initialize_output(::typeof(svd_compact!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_cod = V_dom = infimum(fuse(codomain(t)), fuse(domain(t)))
        U = similar(t, codomain(t) ← V_cod)
        S = similar_diagonal(t, real(scalartype(t)), V_cod)
        Vᴴ = similar(t, V_dom ← domain(t))
        return U, S, Vᴴ
    end
end

function MAK.initialize_output(::typeof(svd_vals!), t::AbstractTensorMap, alg::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_cod = infimum(fuse(codomain(t)), fuse(domain(t)))
        T = real(scalartype(t))
        A = similarstoragetype(t, T)
        return SectorVector{T, sectortype(t), A}(undef, V_cod)
    end
end

# Eigenvalue decomposition
# ------------------------
function MAK.initialize_output(::typeof(eigh_full!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_D = fuse(domain(t))
        D = similar_diagonal(t, real(scalartype(t)), V_D)
        V = similar(t, codomain(t) ← V_D)
        return D, V
    end
end

function MAK.initialize_output(::typeof(eig_full!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_D = fuse(domain(t))
        Tc = complex(scalartype(t))
        D = similar_diagonal(t, Tc, V_D)
        V = similar(t, Tc, codomain(t) ← V_D)
        return D, V
    end
end

function MAK.initialize_output(::typeof(eigh_vals!), t::AbstractTensorMap, alg::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_D = fuse(domain(t))
        T = real(scalartype(t))
        A = similarstoragetype(t, T)
        return SectorVector{T, sectortype(t), A}(undef, V_D)
    end
end

function MAK.initialize_output(::typeof(eig_vals!), t::AbstractTensorMap, alg::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_D = fuse(domain(t))
        Tc = complex(scalartype(t))
        A = similarstoragetype(t, Tc)
        return SectorVector{Tc, sectortype(t), A}(undef, V_D)
    end
end

# QR decomposition
# ----------------
function MAK.initialize_output(::typeof(qr_full!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_Q = fuse(codomain(t))
        Q = similar(t, codomain(t) ← V_Q)
        R = similar(t, V_Q ← domain(t))
        return Q, R
    end
end

function MAK.initialize_output(::typeof(qr_compact!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_Q = infimum(fuse(codomain(t)), fuse(domain(t)))
        Q = similar(t, codomain(t) ← V_Q)
        R = similar(t, V_Q ← domain(t))
        return Q, R
    end
end

function MAK.initialize_output(::typeof(qr_null!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_Q = infimum(fuse(codomain(t)), fuse(domain(t)))
        V_N = ⊖(fuse(codomain(t)), V_Q)
        N = similar(t, codomain(t) ← V_N)
        return N
    end
end

# LQ decomposition
# ----------------
function MAK.initialize_output(::typeof(lq_full!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_Q = fuse(domain(t))
        L = similar(t, codomain(t) ← V_Q)
        Q = similar(t, V_Q ← domain(t))
        return L, Q
    end
end

function MAK.initialize_output(::typeof(lq_compact!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_Q = infimum(fuse(codomain(t)), fuse(domain(t)))
        L = similar(t, codomain(t) ← V_Q)
        Q = similar(t, V_Q ← domain(t))
        return L, Q
    end
end

function MAK.initialize_output(::typeof(lq_null!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        V_Q = infimum(fuse(codomain(t)), fuse(domain(t)))
        V_N = ⊖(fuse(domain(t)), V_Q)
        N = similar(t, V_N ← domain(t))
        return N
    end
end

# Polar decomposition
# -------------------
function MAK.initialize_output(::typeof(left_polar!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        W = similar(t, space(t))
        P = similar(t, domain(t) ← domain(t))
        return W, P
    end
end

function MAK.initialize_output(::typeof(right_polar!), t::AbstractTensorMap, ::AbstractAlgorithm)
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" begin
        P = similar(t, codomain(t) ← codomain(t))
        Wᴴ = similar(t, space(t))
        return P, Wᴴ
    end
end

# Projections
# -----------
MAK.initialize_output(::typeof(project_hermitian!), tsrc::AbstractTensorMap, ::AbstractAlgorithm) =
    tsrc
MAK.initialize_output(::typeof(project_antihermitian!), tsrc::AbstractTensorMap, ::AbstractAlgorithm) =
    tsrc
MAK.initialize_output(::typeof(project_isometric!), tsrc::AbstractTensorMap, ::AbstractAlgorithm) =
    @timeit_debug GLOBAL_TIMER "alloc: initialize_output" similar(tsrc)

# Exponential
# ----------------
MAK.initialize_output(::typeof(exponential!), t::AbstractTensorMap, ::AbstractAlgorithm) = t
MAK.initialize_output(::typeof(exponential!), (τ, t)::Tuple{Number, AbstractTensorMap}, ::AbstractAlgorithm) = t
MAK.initialize_output(::typeof(exponential!), (τ, t)::Tuple{T1, AbstractTensorMap{T2}}, ::AbstractAlgorithm) where {T1 <: Complex, T2 <: Real} = similar(t, complex(eltype(t)))
