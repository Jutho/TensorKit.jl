# =============
#  Reweighting
# =============

# ------
# flip
# ------
"""
    flip(t::AbstractTensorMap, I; inv::Bool = false) -> t′::AbstractTensorMap

Return a new tensor that is isomorphic to `t` but where the arrows on the indices `i` that satisfy
`i ∈ I` are flipped, i.e. `space(t′, i) = flip(space(t, i))`.

!!! note
    The isomorphism that `flip` applies to each of the indices `i ∈ I` is such that flipping two indices
    that are afterwards contracted within an `@tensor` contraction will yield the same result as without
    flipping those indices first. However, `flip` is not involutory, i.e. `flip(flip(t, I), I) != t` in
    general. To obtain the original tensor, one can use the `inv` keyword, i.e. it holds that
    `flip(flip(t, I), I; inv=true) == t`.
"""
function flip(t::AbstractTensorMap, I; inv::Bool = false)
    P = flip(space(t), I)
    t′ = similar(t, promote_flip(t), P)
    for (f₁, f₂) in fusiontrees(t)
        (f₁′, f₂′), factor = only(flip((f₁, f₂), I; inv))
        scale!(t′[f₁′, f₂′], t[f₁, f₂], factor)
    end
    return t′
end

# ---------
# twist(!)
# ---------
function has_shared_twist(t, inds)
    I = sectortype(t)
    if BraidingStyle(I) == NoBraiding()
        for i in inds
            cs = sectors(space(t, i))
            all(isunit, cs) || throw(SectorMismatch(lazy"Cannot twist sectors $cs"))
        end
        return true
    elseif BraidingStyle(I) == Bosonic()
        return true
    else
        for i in inds
            cs = sectors(space(t, i))
            all(isone ∘ twist, cs) || return false
        end
        return true
    end
end

"""
    twist!(t::AbstractTensorMap, i::Int; inv::Bool = false) -> t
    twist!(t::AbstractTensorMap, inds; inv::Bool = false) -> t

Apply a twist to the `i`th index of `t`, or all indices in `inds`, storing the result in `t`.
If `inv=true`, use the inverse twist.

See [`twist`](@ref) for creating a new tensor.
"""
function twist!(t::AbstractTensorMap, inds; inv::Bool = false)
    if !all(in(allind(t)), inds)
        msg = "Can't twist indices $inds of a tensor with only $(numind(t)) indices."
        throw(ArgumentError(msg))
    end
    (scalartype(t) <: Real && !(sectorscalartype(sectortype(t)) <: Real)) &&
        throw(ArgumentError("Can't in-place twist a real tensor with complex sector type"))
    has_shared_twist(t, inds) && return t

    N₁ = numout(t)
    for (f₁, f₂) in fusiontrees(t)
        θ = prod(i -> i <= N₁ ? twist(f₁.uncoupled[i]) : twist(f₂.uncoupled[i - N₁]), inds)
        inv && (θ = θ')
        scale!(t[f₁, f₂], θ)
    end
    return t
end

"""
    twist(tsrc::AbstractTensorMap, i::Int; inv::Bool = false, copy::Bool = false) -> tdst
    twist(tsrc::AbstractTensorMap, inds; inv::Bool = false, copy::Bool = false) -> tdst

Apply a twist to the `i`th index of `tsrc` and return the result as a new tensor.
If `inv = true`, use the inverse twist.
If `copy = false`, `tdst` might share data with `tsrc` whenever possible. Otherwise, a copy is always made.

See [`twist!`](@ref) for storing the result in place.
"""
function twist(t::AbstractTensorMap, inds; inv::Bool = false, copy::Bool = false)
    if has_shared_twist(t, inds)
        return copy ? Base.copy(t) : t
    end
    tdst = similar(t, promote_twist(t))
    copy!(tdst, t)
    return twist!(tdst, inds; inv)
end

# =========================
#  Space insertion/removal
# =========================

# Methods which change the number of indices, implement using `Val(i)` for type inference
"""
    insertleftunit(
            tsrc::AbstractTensorMap, i = numind(t) + 1;
            conj = false, dual = false, copy = false
        ) -> tdst

Insert a trivial vector space, isomorphic to the underlying field, at position `i`,
which can be specified as an `Int` or as `Val(i)` for improved type stability.
More specifically, adds a left monoidal unit or its dual.
Insert a trivial vector space, isomorphic to the underlying field, before position `i`,
which should satisfy `1 ≤ i ≤ numind(t) + 1`
and can be specified as an `Int` or as `Val(i)` for improved type stability,
More specifically, add a left monoidal unit (or its dual) of the space associated with index `i`.
The new index appears at position `i` in the new tensor,
namely in its codomain for `1 ≤ i ≤ numout(t)` and in its domain otherwise.
If `copy=false`, `tdst` might share data with `tsrc` whenever possible. Otherwise, a copy is always made.

See also [`insertrightunit`](@ref insertrightunit(::AbstractTensorMap, ::Val{i}) where {i}),
[`removeunit`](@ref removeunit(::AbstractTensorMap, ::Val{i}) where {i}).
"""
function insertleftunit(
        t::AbstractTensorMap, ::Val{i} = Val(numind(t) + 1);
        copy::Bool = false, conj::Bool = false, dual::Bool = false
    ) where {i}
    W = insertleftunit(space(t), Val(i); conj, dual)
    if t isa TensorMap
        return TensorMap{scalartype(t)}(copy ? Base.copy(t.data) : t.data, W)
    else
        tdst = similar(t, W)
        for (c, b) in blocks(t)
            copy!(block(tdst, c), b)
        end
        return tdst
    end
end

"""
    insertrightunit(
            tsrc::AbstractTensorMap, i = numind(t);
            conj = false, dual = false, copy = false
        ) -> tdst

Insert a trivial vector space, isomorphic to the underlying field, after position `i`,
which should satisfy `0 ≤ i ≤ numind(t)`
and can be specified as an `Int` or as `Val(i)` for improved type stability,
More specifically, add a right monoidal unit (or its dual) of the space associated with index `i`.
The new index appears at position `i+1` in the new tensor,
namely in its codomain for `0 ≤ i ≤ numout(t)` and in its domain otherwise.

If `copy=false`, `tdst` might share data with `tsrc` whenever possible. Otherwise, a copy is always made.

See also [`insertleftunit`](@ref insertleftunit(::AbstractTensorMap, ::Val{i}) where {i}),
[`removeunit`](@ref removeunit(::AbstractTensorMap, ::Val{i}) where {i}).
"""
function insertrightunit(
        t::AbstractTensorMap, ::Val{i} = Val(numind(t));
        copy::Bool = false, conj::Bool = false, dual::Bool = false
    ) where {i}
    W = insertrightunit(space(t), Val(i); conj, dual)
    if t isa TensorMap
        return TensorMap{scalartype(t)}(copy ? Base.copy(t.data) : t.data, W)
    else
        tdst = similar(t, W)
        for (c, b) in blocks(t)
            copy!(block(tdst, c), b)
        end
        return tdst
    end
end

"""
    removeunit(tsrc::AbstractTensorMap, i; copy = false) -> tdst

This removes a trivial tensor product factor at position `1 ≤ i ≤ N`, where `i`
can be specified as an `Int` or as `Val(i)` for improved type stability.
For this to work, that factor has to be isomorphic to the field of scalars.

If `copy=false`, `tdst` might share data with `tsrc` whenever possible. Otherwise, a copy is always made.

This operation undoes the work of [`insertleftunit`](@ref insertleftunit(::AbstractTensorMap, ::Val{i}) where {i})
and [`insertrightunit`](@ref insertrightunit(::AbstractTensorMap, ::Val{i}) where {i}).
"""
function removeunit(t::AbstractTensorMap, ::Val{i}; copy::Bool = false) where {i}
    W = removeunit(space(t), Val(i))
    if t isa TensorMap
        return TensorMap{scalartype(t)}(copy ? Base.copy(t.data) : t.data, W)
    else
        tdst = similar(t, W)
        for (c, b) in blocks(t)
            copy!(block(tdst, c), b)
        end
        return tdst
    end
end

# TODO: fusion/splitting of indices

# ============================
# Index rearrangements
# ============================

# --------------
#   permute(!)
# --------------
"""
    permute!(tdst, tsrc, (p₁, p₂)::Index2Tuple, α = 1, β = 0, [backend], [allocator]) -> tdst

Compute `tdst = β * tdst + α * permute(tsrc, (p₁, p₂))`, writing the result into `tdst`.
The codomain and domain of `tdst` correspond to the indices in `p₁` and `p₂` of `tsrc` respectively.
Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`permute`](@ref) for creating a new tensor.
"""
@propagate_inbounds function Base.permute!(
        tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple,
        α::Number = One(), β::Number = Zero(),
        backend::AbstractBackend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    @boundscheck spacecheck_transform(permute, tdst, tsrc, p)
    @timeit_debug GLOBAL_TIMER "permute!/braid!" begin
        tdst′, tsrc′, p′, _, conjsrc, α′, β′ = unwrap_adjoints(tdst, tsrc, p, nothing, false, α, β)
        @inbounds _braid!(tdst′, tsrc′, p′, conjsrc, allind(tsrc′), α′, β′, backend, allocator)
    end
    return tdst
end

"""
    permute(
        tsrc, (p₁, p₂)::Index2Tuple; copy = false,
        backend = DefaultBackend(), allocator = DefaultAllocator()
    ) -> tdst::TensorMap    

Return tensor `tdst` obtained by permuting the indices of `tsrc`.
The codomain and domain of `tdst` correspond to the indices in `p₁` and `p₂` of `tsrc` respectively.

If `copy = false`, `tdst` might share data with `tsrc` whenever possible.
Otherwise, a copy is always made.
Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`permute!`](@ref) for writing into an existing destination.
"""
function permute(
        t::AbstractTensorMap, p::Index2Tuple;
        copy::Bool = false, backend::AbstractBackend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    # share data if possible
    if !copy
        if p == (codomainind(t), domainind(t))
            return t
        elseif t isa TensorMap && has_shared_permute(t, p)
            return TensorMap(t.data, permute(space(t), p))
        end
    end

    # general case
    tdst = similar(t, promote_permute(t), permute(space(t), p))
    return @inbounds permute!(tdst, t, p, One(), Zero(), backend, allocator)
end
function permute(t::AdjointTensorMap, (p₁, p₂)::Index2Tuple; kwargs...)
    p₁′ = adjointtensorindices(t, p₂)
    p₂′ = adjointtensorindices(t, p₁)
    return adjoint(permute(adjoint(t), (p₁′, p₂′); kwargs...))
end
permute(t::AbstractTensorMap, p::IndexTuple; kwargs...) = permute(t, (p, ()); kwargs...)

function has_shared_permute(t::AbstractTensorMap, (p₁, p₂)::Index2Tuple)
    return (p₁ === codomainind(t) && p₂ === domainind(t))
end
function has_shared_permute(t::TensorMap, (p₁, p₂)::Index2Tuple)
    if p₁ === codomainind(t) && p₂ === domainind(t)
        return true
    elseif sectortype(t) === Trivial
        stridet = Base.Fix1(stride, t[])
        sizet = Base.Fix1(size, t[])
        canfuse1, d1, s1 = TO._canfuse(sizet.(p₁), stridet.(p₁))
        canfuse2, d2, s2 = TO._canfuse(sizet.(p₂), stridet.(p₂))
        return canfuse1 && canfuse2 && s1 == 1 && (d2 == 1 || s2 == d1)
    else
        return false
    end
end
function has_shared_permute(t::AdjointTensorMap, (p₁, p₂)::Index2Tuple)
    p₁′ = adjointtensorindices(t, p₂)
    p₂′ = adjointtensorindices(t, p₁)
    return has_shared_permute(t', (p₁′, p₂′))
end

# -------------
#   braid(!)
# -------------
"""
    braid!(tdst, tsrc, (p₁, p₂)::Index2Tuple, levels::IndexTuple, α = 1, β = 0, [backend], [allocator]) -> tdst

Compute `tdst = β * tdst + α * braid(tsrc, (p₁, p₂), levels)`, writing the result into `tdst`.
The codomain and domain of `tdst` correspond to the indices in `p₁` and `p₂` of `tsrc` respectively.
Here, `levels` is a tuple of length `numind(tsrc)` that assigns a level or depth to the indices of `tsrc`,
which determines whether they will braid over or under any other index with which they have to change places.
In other words, a smaller value in `levels` means that the corresponding index will be braided over any other index with a higher value.

Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`braid`](@ref) for creating a new tensor.
"""
@propagate_inbounds function braid!(
        tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple, levels::IndexTuple,
        α::Number = One(), β::Number = Zero(),
        backend::AbstractBackend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    @boundscheck spacecheck_transform(braid, tdst, tsrc, p, levels)
    @timeit_debug GLOBAL_TIMER "permute!/braid!" begin
        tdst′, tsrc′, p′, levels′, conjsrc, α′, β′ = unwrap_adjoints(tdst, tsrc, p, levels, false, α, β)
        @inbounds _braid!(tdst′, tsrc′, p′, conjsrc, levels′, α′, β′, backend, allocator)
    end
    return tdst
end

"""
    braid(
        tsrc, (p₁, p₂)::Index2Tuple, levels::IndexTuple; copy = false,
        backend = DefaultBackend(), allocator = DefaultAllocator()
    ) -> tdst::TensorMap

Return tensor `tdst` obtained by braiding the indices of `tsrc`.
The codomain and domain of `tdst` correspond to the indices in `p₁` and `p₂` of `tsrc` respectively.
Here, `levels` is a tuple of length `numind(tsrc)` that assigns a level or depth to the indices of `tsrc`,
which determines whether they will braid over or under any other index with which they have to change places.
In other words, a smaller value in `levels` means that the corresponding index will be braided over any other index with a higher value.

If `copy=false`, `tdst` might share data with `tsrc` whenever possible. Otherwise, a copy is always made.
Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`braid!`](@ref) for writing into an existing destination.
"""
function braid(
        t::AbstractTensorMap, p::Index2Tuple, levels::IndexTuple;
        copy::Bool = false, backend::AbstractBackend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    length(levels) == numind(t) || throw(ArgumentError(lazy"length of levels should be $(numind(t)), got $(length(levels))"))

    (!copy && p == (codomainind(t), domainind(t))) && return t

    # general case
    tdst = similar(t, promote_braid(t), permute(space(t), p))
    return @inbounds braid!(tdst, t, p, levels, One(), Zero(), backend, allocator)
end
function braid(
        t::AdjointTensorMap, (p₁, p₂)::Index2Tuple, levels::IndexTuple;
        kwargs...
    )
    p₁′ = adjointtensorindices(t, p₂)
    p₂′ = adjointtensorindices(t, p₁)
    perm = adjointtensorindices(adjoint(t), ntuple(identity, numind(t)))
    levels′ = TupleTools.getindices(levels, perm)
    return adjoint(braid(adjoint(t), (p₁′, p₂′), levels′; kwargs...))
end

# ----------------
#   transpose(!)
# ----------------
_transpose_indices(t::AbstractTensorMap) = (reverse(domainind(t)), reverse(codomainind(t)))

"""
    transpose!(tdst, tsrc, (p₁, p₂)::Index2Tuple, α = 1, β = 0, [backend], [allocator]) -> tdst

Compute `tdst = β * tdst + α * transpose(tsrc, (p₁, p₂))`, writing the result into `tdst`.
The codomain and domain of `tdst` correspond to the indices in `p₁` and `p₂` of `tsrc` respectively.
The new index positions should be attainable without any indices crossing each other, i.e.,
the permutation `(p₁..., reverse(p₂)...)` should constitute a cyclic permutation of
`(codomainind(tsrc)..., reverse(domainind(tsrc))...)`.
Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`transpose`](@ref) for creating a new tensor.
"""
function LinearAlgebra.transpose!(tdst::AbstractTensorMap, tsrc::AbstractTensorMap)
    return transpose!(tdst, tsrc, _transpose_indices(tsrc))
end
@propagate_inbounds function LinearAlgebra.transpose!(
        tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple,
        α::Number = One(), β::Number = Zero(),
        backend::AbstractBackend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    @boundscheck spacecheck_transform(transpose, tdst, tsrc, p)
    @timeit_debug GLOBAL_TIMER "transpose!" begin
        tdst′, tsrc′, p′, _, conjsrc, α′, β′ = unwrap_adjoints(tdst, tsrc, p, nothing, false, α, β)
        @inbounds _transpose!(tdst′, tsrc′, p′, conjsrc, α′, β′, backend, allocator)
    end
    return tdst
end

"""
    transpose(
        tsrc, (p₁, p₂)::Index2Tuple; copy = false,
        backend = DefaultBackend(), allocator = DefaultAllocator()
    ) -> tdst::TensorMap

Return tensor `tdst` obtained by transposing the indices of `tsrc`.
The codomain and domain of `tdst` correspond to the indices in `p₁` and `p₂` of `tsrc` respectively.
The new index positions should be attainable without any indices crossing each other, i.e.,
the permutation `(p₁..., reverse(p₂)...)` should constitute a cyclic permutation of
`(codomainind(tsrc)..., reverse(domainind(tsrc))...)`.

If `copy=false`, `tdst` might share data with `tsrc` whenever possible. Otherwise, a copy is always made.
Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`transpose!`](@ref) for writing into an existing destination.
"""
function LinearAlgebra.transpose(
        t::AbstractTensorMap, p::Index2Tuple = _transpose_indices(t);
        copy::Bool = false, backend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    sectortype(t) === Trivial && return permute(t, p; copy, backend, allocator)
    (!copy && p == (codomainind(t), domainind(t))) && return t

    # general case
    tdst = similar(t, promote_transpose(t), permute(space(t), p))
    return @inbounds transpose!(tdst, t, p, One(), Zero(), backend, allocator)
end

function LinearAlgebra.transpose(
        t::AdjointTensorMap, (p₁, p₂)::Index2Tuple = _transpose_indices(t);
        copy::Bool = false, backend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    p₁′ = map(n -> adjointtensorindex(t, n), p₂)
    p₂′ = map(n -> adjointtensorindex(t, n), p₁)
    return adjoint(transpose(adjoint(t), (p₁′, p₂′); copy, backend, allocator))
end

# -------------------
#   repartition(!)
# -------------------
"""
    repartition!(tdst, tsrc, α = 1, β = 0, [backend], [allocator]) -> tdst

Compute `tdst = β * tdst + α * repartition(tsrc)`, writing the result into `tdst`.
This is a special case of `transpose!` that only changes the partition of indices between
codomain and domain, without changing their cyclic order.
Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`repartition`](@ref) for creating a new tensor.
"""
@propagate_inbounds function repartition!(
        tdst::AbstractTensorMap, tsrc::AbstractTensorMap,
        α::Number = One(), β::Number = Zero(),
        backend::AbstractBackend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    check_spacetype(tdst, tsrc)
    numind(tsrc) == numind(tdst) ||
        throw(ArgumentError("tsrc and tdst should have an equal amount of indices"))
    p₁, p₂ = let all_inds = (codomainind(tsrc)..., reverse(domainind(tsrc))...)
        ntuple(i -> all_inds[i], numout(tdst)), reverse(ntuple(i -> all_inds[i + numout(tdst)], numin(tdst)))
    end
    return transpose!(tdst, tsrc, (p₁, p₂), α, β, backend, allocator)
end

"""
    repartition(
            tsrc, N₁::Int, N₂::Int = numind(tsrc) - N₁; copy = false,
            backend = DefaultBackend(), allocator = DefaultAllocator()
        ) -> tdst

Return tensor `tdst` obtained by repartitioning the indices of `tsrc`.
The codomain and domain of `tdst` correspond to the first `N₁` and last `N₂` spaces of `tsrc`,
respectively.

If `copy=false`, `tdst` might share data with `tsrc` whenever possible. Otherwise, a copy is always made.
Optionally specify a `backend` and `allocator` for the underlying array operation.

See also [`repartition!`](@ref) for writing into an existing destination.
"""
@constprop :aggressive function repartition(
        t::AbstractTensorMap, N₁::Int, N₂::Int = numind(t) - N₁;
        copy::Bool = false, backend = TO.DefaultBackend(), allocator = TO.DefaultAllocator()
    )
    N₁ + N₂ == numind(t) ||
        throw(ArgumentError(lazy"Invalid repartition: $(numind(t)) to ($N₁, $N₂)"))
    p₁, p₂ = let all_inds = (codomainind(t)..., reverse(domainind(t))...)
        ntuple(i -> all_inds[i], N₁), reverse(ntuple(i -> all_inds[i + N₁], N₂))
    end
    return transpose(t, (p₁, p₂); copy, backend, allocator)
end

#-------------------------------------
# Internal implementations
#-------------------------------------

# find the scalartype after applying operations: take into account fusion and/or braiding
# might need to become Float or Complex to capture complex recoupling coefficients but don't alter precision
for (operation, manipulation) in (
        :flip => :sector, :twist => :braiding,
        :transpose => :fusion, :permute => :sector, :braid => :sector,
    )
    promote_op = Symbol(:promote_, operation)
    manipulation_scalartype = Symbol(manipulation, :scalartype)

    @eval begin
        $promote_op(t::AbstractTensorMap) = $promote_op(typeof(t))
        $promote_op(::Type{T}) where {T <: AbstractTensorMap} =
            $promote_op(scalartype(T), sectortype(T))
        $promote_op(::Type{T}, ::Type{I}) where {T <: Number, I <: Sector} =
            $manipulation_scalartype(I) <: Integer ? T :
            $manipulation_scalartype(I) <: Real ? float(T) : complex(T)
    end
end

spacecheck_transform(f, tdst::AbstractTensorMap, tsrc::AbstractTensorMap, args...) =
    spacecheck_transform(f, space(tdst), space(tsrc), args...)
@noinline function spacecheck_transform(f, Vdst::TensorMapSpace, Vsrc::TensorMapSpace, p::Index2Tuple)
    check_spacetype(Vdst, Vsrc)
    f(Vsrc, p) == Vdst ||
        throw(
        SpaceMismatch(
            lazy"""
            incompatible spaces for `$f(Vsrc, $p) -> Vdst`
            Vsrc = $Vsrc
            Vdst = $Vdst
            """
        )
    )
    return nothing
end
@noinline function spacecheck_transform(::typeof(braid), Vdst::TensorMapSpace, Vsrc::TensorMapSpace, p::Index2Tuple, levels::IndexTuple)
    check_spacetype(Vdst, Vsrc)
    braid(Vsrc, p, levels) == Vdst ||
        throw(
        SpaceMismatch(
            lazy"""
            incompatible spaces for `braid(Vsrc, $p, $levels) -> Vdst`
            Vsrc = $Vsrc
            Vdst = $Vdst
            """
        )
    )
    return nothing
end

# Deprecated add_*! wrappers
# --------------------------
Base.@deprecate(
    add_permute!(tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple, α::Number, β::Number, backend::AbstractBackend...),
    permute!(tdst, tsrc, p, α, β, backend...)
)
Base.@deprecate(
    add_braid!(tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple, levels::IndexTuple, α::Number, β::Number, backend::AbstractBackend...),
    braid!(tdst, tsrc, p, levels, α, β, backend...)
)
Base.@deprecate(
    add_transpose!(tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple, α::Number, β::Number, backend::AbstractBackend...),
    transpose!(tdst, tsrc, p, α, β, backend...)
)

# Kernel implementation
# ---------------------
# Index manipulations are expressed as `tdst = β * tdst + α * permutedims(conjsrc ? conj(tsrc) : tsrc, p)`,
# with `p` indexing the legs of `tsrc`. Adjoint wrappers are absorbed into `conjsrc`, `p`, `levels`
# and the scalars, such that the actual work always happens on the subblocks of the parent tensors.

# levels are attached to the legs: relabel them from the legs of `t'` to those of `t`
_adjoint_levels(t, ::Nothing) = nothing
_adjoint_levels(t, levels::IndexTuple) = TupleTools.getindices(levels, adjointtensorindices(t, allind(t)))

_unwrap_source(tsrc::AbstractTensorMap, p, levels, conjsrc::Bool) = (tsrc, p, levels, conjsrc)
function _unwrap_source(tsrc::AdjointTensorMap, p, levels, conjsrc::Bool)
    tp = parent(tsrc)
    return (tp, adjointtensorindices(tsrc, p), _adjoint_levels(tp, levels), !conjsrc)
end

_unwrap_destination(tdst::AbstractTensorMap, p, conjsrc::Bool, α, β) = (tdst, p, conjsrc, α, β)
function _unwrap_destination(tdst::AdjointTensorMap, p, conjsrc::Bool, α, β)
    return (parent(tdst), (p[2], p[1]), !conjsrc, conj(α), conj(β))
end

"""
    unwrap_adjoints(tdst, tsrc, p, levels, conjsrc::Bool, α, β) -> (tdst′, tsrc′, p′, levels′, conjsrc′, α′, β′)

Rewrite the operation `tdst = β * tdst + α * braid(conjsrc ? conj(tsrc) : tsrc, p, levels)` such that
neither `tdst′` nor `tsrc′` is an `AdjointTensorMap`, by absorbing the adjoints into the conjugation
flag, the permutation, the `levels` (which may be `nothing`) and the scalars.
"""
function unwrap_adjoints(tdst, tsrc, p::Index2Tuple, levels, conjsrc::Bool, α, β)
    tsrc′, p′, levels′, conjsrc′ = _unwrap_source(tsrc, p, levels, conjsrc)
    tdst′, p″, conjsrc″, α′, β′ = _unwrap_destination(tdst, p′, conjsrc′, α, β)
    return (tdst′, tsrc′, p″, levels′, conjsrc″, α′, β′)
end

# dense transform that bypasses overhead
function _dense_transform!(tdst, tsrc, p::Index2Tuple, conjsrc::Bool, α, β, backend, allocator)
    p2 = (linearize(p), ()) # only the linear permutation matters for the array kernels
    @timeit_debug GLOBAL_TIMER "dense: tensoradd" TO.tensoradd!(
        tdst[], tsrc[], p2, conjsrc, α, β, backend, allocator
    )
    return tdst
end

# space check for `tdst = permutedims(conjsrc ? conj(tsrc) : tsrc, p)`
function spacecheck_transform(f, tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple, conjsrc::Bool)
    Vsrc′, p′ = transform_source(space(tsrc), p, conjsrc)
    return spacecheck_transform(f, space(tdst), Vsrc′, p′)
end

@propagate_inbounds function _braid!(
        tdst, tsrc, p::Index2Tuple, conjsrc::Bool, levels::IndexTuple, α, β, backend, allocator
    )
    @boundscheck spacecheck_transform(permute, tdst, tsrc, p, conjsrc)
    has_array_view(tdst, tsrc) && return _dense_transform!(tdst, tsrc, p, conjsrc, α, β, backend, allocator)
    transformer = treebraider(tdst, tsrc, p, conjsrc, levels)
    return @inbounds add_transform!(tdst, tsrc, p, conjsrc, transformer, α, β, backend, allocator)
end

# counterpart of `_braid!` for `transpose!`; the cyclicity of `p` is checked by the caller
@propagate_inbounds function _transpose!(
        tdst, tsrc, p::Index2Tuple, conjsrc::Bool, α, β, backend, allocator
    )
    @boundscheck spacecheck_transform(permute, tdst, tsrc, p, conjsrc)
    has_array_view(tdst, tsrc) && return _dense_transform!(tdst, tsrc, p, conjsrc, α, β, backend, allocator)
    transformer = treetransposer(tdst, tsrc, p, conjsrc)
    return @inbounds add_transform!(tdst, tsrc, p, conjsrc, transformer, α, β, backend, allocator)
end

"""
    add_transform!(tdst, tsrc, p, conjsrc::Bool, transformer, α, β, backend, allocator) -> tdst

Compute `tdst = β * tdst + α * permutedims(conjsrc ? conj(tsrc) : tsrc, p)`, where `p` indexes the legs
of `tsrc`, using the fusion tree transformation encoded in `transformer` (see [`TreeTransformer`](@ref)).
"""
@propagate_inbounds function add_transform!(
        tdst::AbstractTensorMap, tsrc::AbstractTensorMap, p::Index2Tuple, conjsrc::Bool, transformer,
        α::Number, β::Number, backend, allocator
    )
    @boundscheck spacecheck_transform(permute, tdst, tsrc, p, conjsrc)

    if !conjsrc && p[1] === codomainind(tsrc) && p[2] === domainind(tsrc)
        add!(tdst, tsrc, α, β)
    else
        p2 = (linearize(p), ()) # only the linear permutation matters for the array kernels
        ntasks = use_threaded_transform(tdst, transformer) ? get_num_transformer_threads() : 1
        # resolve the conjugation flag into the view type here, with a statically typed call per branch
        if conjsrc
            dst, src = _transform_subblocks(tdst, tsrc, transformer, conj)
            add_transform_kernel!(dst, src, p2, transformer, α, β, backend, allocator, ntasks)
        else
            dst, src = _transform_subblocks(tdst, tsrc, transformer, identity)
            add_transform_kernel!(dst, src, p2, transformer, α, β, backend, allocator, ntasks)
        end
    end

    return tdst
end

# TensorMaps address their flat data directly, other tensor types go through `subblock`
_transform_subblocks(tdst::TensorMap, tsrc::TensorMap, transformer, op) =
    StridedSubblocks(tdst, transformer.structure_dst), StridedSubblocks(tsrc, transformer.structure_src, op)
_transform_subblocks(tdst::AbstractTensorMap, tsrc::AbstractTensorMap, transformer, op) =
    TreeSubblocks(tdst), TreeSubblocks(tsrc, op)

use_threaded_transform(t::TensorMap, transformer) =
    get_num_transformer_threads() > 1 && length(t.data) > Strided.MINTHREADLENGTH
use_threaded_transform(t::AbstractTensorMap, transformer) =
    get_num_transformer_threads() > 1 && dim(space(t)) > Strided.MINTHREADLENGTH

# The kernel operates on the subblocks addressed by position, so that for `TensorMap`s this only
# depends on `numind`, `eltype` and the transformer data, not on the sectortype.
const TransformSubblocks = Union{StridedSubblocks, TreeSubblocks}
function add_transform_kernel!(
        dst::TransformSubblocks, src::TransformSubblocks, p,
        transformer::Union{AbelianTreeTransformer, GenericTreeTransformer},
        α, β, backend, allocator, ntasks::Int
    )
    bufsize = buffersize(transformer)
    if bufsize == 0 # no recoupling needed: every block consists of a single tree
        taskforeach(transformer.data, ntasks) do (U, inds_dst, inds_src)
            _add_transform_block!(dst, src, p, U, inds_dst, inds_src, nothing, α, β, backend, allocator)
        end
    else
        # One max-sized workspace per task (a single one that is reused by all blocks when
        # serial), allocated on the calling thread before any task spawns, so that also
        # allocators that are not thread-safe can be used.
        cp = TO.allocator_checkpoint!(allocator)
        @timeit_debug GLOBAL_TIMER "alloc: buffers" buffers = [
            TO.tensoralloc(storagetype(dst), bufsize, Val(true), allocator)
                for _ in 1:clamp(length(transformer.data), 1, ntasks)
        ]
        taskforeach(transformer.data, buffers) do (U, inds_dst, inds_src), buffer
            _add_transform_block!(dst, src, p, U, inds_dst, inds_src, buffer, α, β, backend, allocator)
        end
        foreach(Base.Fix2(TO.tensorfree!, allocator), buffers)
        TO.allocator_reset!(allocator, cp)
    end
    return nothing
end

# `U` is either a scalar coefficient with integer positions (abelian), or a recoupling matrix
# with vectors of positions (generic).
function _add_transform_block!(
        dst::TransformSubblocks, src::TransformSubblocks, p, U, inds_dst, inds_src, buffer,
        α, β, backend, allocator
    )
    if length(U) == 1 # single tree: no matmul needed
        @timeit_debug GLOBAL_TIMER "dense: tensoradd" @inbounds TO.tensoradd!(
            dst[only(inds_dst)], src[only(inds_src)], p, false, α * only(U), β, backend, allocator
        )
    else # Multi-tree block: pack → recoupling matmul → unpack.
        rows, cols = size(U)
        sz_src = size(@inbounds(src[first(inds_src)]))
        blocksize = prod(sz_src)
        ptriv = (ntuple(identity, length(sz_src)), ())
        buffer_dst = StridedView(buffer, (blocksize, rows), (1, blocksize), 0)
        buffer_src = StridedView(buffer, (blocksize, cols), (1, blocksize), blocksize * rows)

        # 1. Extract: copy each source block into column i of buffer_src as a flat vector,
        #    using a trivial permutation so the layout is canonical before the matmul.
        @timeit_debug GLOBAL_TIMER "dense: pack" @inbounds for (i, isrc) in enumerate(inds_src)
            TO.tensoradd!(
                sreshape(view(buffer_src, :, i), sz_src), src[isrc],
                ptriv, false, One(), Zero(), backend, allocator
            )
        end

        # 2. Recoupling: buffer_dst = α * buffer_src * U^T  (each output tree is a linear
        #    combination of input trees weighted by the recoupling coefficients).
        @timeit_debug GLOBAL_TIMER "dense: recouple mul!" begin
            U′ = _adapt_recoupling(storagetype(dst), U)
            mul!(buffer_dst, buffer_src, transpose(U′), α, Zero())
        end

        # 3. Insert: scatter column j of buffer_dst into the destination, applying the
        #    actual index permutation p in the same tensoradd! call.
        @timeit_debug GLOBAL_TIMER "dense: unpack" @inbounds for (j, idst) in enumerate(inds_dst)
            TO.tensoradd!(
                dst[idst], sreshape(view(buffer_dst, :, j), sz_src),
                p, false, One(), β, backend, allocator
            )
        end
    end
    return nothing
end
